

library("Meteorology", 0, 0, 1) {
	
	#TODO: Better documentation, esp. of units!
	
	latent_heat_of_vaporization : function(T_celsius) {   # Harrison 1963
		2.501 - 2.361e-3 * T_celsius
	}
	
	mean_barometric_pressure : function(elevation)    {   # Doorenbos, Pruitt 1977
		101.3 - elev*(0.01152 - 0.544e-6*elev)
	}
	
	psychrometric_constant : function(barom, dhvap)   {   # Brunt 1952
		1.013e-3 * barom / (0.622 * dhvap)
	}
	
	saturation_vapor_pressure_murray : function(T_celsius)   {   # Tetens 1930, Murray 1967
		exp( (16.78*T_celsius - 116.9) / (T_celsius + 237.3) )
	}
	
	saturation_vapor_pressure_lowe : function(T_celsius)   {
		#P. R. Lowe, 1977, "An approximating polynomial for the computation of saturation vapor pressure, J. Appl. Meteor., 16, 100-103.
		#Returns saturation vapor pressure in millibar=hectopascal
		#TODO: that paper has a separate formula for T < 0
		
		a0 := 6.107799961
		a1 := 4.436518521e-1
		a2 := 1.428945805e-2
		a3 := 2.650648471e-4
		a4 := 3.031240396e-6
		a5 := 2.034080948e-8
		a6 := 6.136820929e-11
		t  := T_celsius
		(a0 + t*(a1 + t*(a2 + t*(a3 + t*(a4 + t*(a5 + t*a6))))))
	}
	
	slope_of_saturation_pressure_curve : function(T_celsius, svap) {
		4098.0 * svap / (T_celsius + 237.3)^2
	}
	
}

library("Radiation", 0, 0, 1) {
	
	solar_constant : constant("Solar constant", unit(W m-2), 1361)
	
	daily_average_extraterrestrial_radiation : function(latitude, day_of_year, days_this_year) {
		inv_rel_dist_earth_sun := 1.0 + 0.033*cos(2*pi*day_of_year/days_this_year)
		solar_declination      := 0.409*sin(2*pi*day_of_year/days_this_year - 1.39)
		lat_rad                := latitude * pi / 180.0
		sunset_hour_angle      := acos(-tan(lat_rad)*tan(solar_declination))
		
		solar_constant * (1/pi) * inv_rel_dist_earth_sun * (sunset_hour_angle * sin(lat_rad) * sin(solar_declination) + cos(lat_rad) * cos(solar_declination) * sin(sunset_hour_angle))
	}
	
	# TODO: Make hourly_average_extraterrestrial_radiation
	
}