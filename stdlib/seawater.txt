
library("Air-sea") {
	
"""
Air-sea/lake heat fluxes are based off of
Air-Sea bulk transfer coefficients in diabatic conditions, Junsei Kondo, 1975, Boundary-Layer Meteorology 9(1), 91-112 [https://doi.org/10.1007/BF00232256](https://doi.org/10.1007/BF00232256)

The implementation used here is influenced by the implementation in [GOTM](https://github.com/gotm-model).
"""
	
	surface_stability : function(wind : [m, s-1], water_temp : [deg_c], air_temp : [deg_c]) {
		ww := wind + 1e-10[m, s-1],
		s0 := 0.25*(water_temp - air_temp) / (ww*ww) => [],
		s0*abs(s0)/(abs(s0) + 0.01)
	}
	
	stab_modify : function(wind : [m, s-1], stab : []) {
		0                                     if abs(wind) < 1e-3[m, s-1],
		0.1 + 0.03*stab * 0.9*exp(4.8*stab)   if stab < 0 & stab > -3.3,
		0                                     if stab < 0,
		1 + 0.63*sqrt(stab)                   otherwise
	}
	
	tc_latent_heat : function(wind : [m, s-1], stability : []) {
		w    := wind=>[] + 1e-12,
		{
			0     + 1.23  *exp(-0.16*ln(w))        if w < 2.2,
			0.969 + 0.0521*w                       if w < 5,
			1.18  + 0.01  *w                       if w < 8,
			1.196 + 0.008 *w - 0.0004*(w-8)^2      if w < 25,
			1.68  - 0.016 *w                       otherwise
		} * 1e-3 * stab_modify(wind, stability)
	}
	
	tc_sensible_heat : function(wind : [m, s-1], stability : []) {
		w    := wind=>[] + 1e-12,
		{
			0     + 1.185 *exp(-0.157*ln(w))       if w < 2.2,
			0.927 + 0.0546*w                       if w < 5,
			1.15  + 0.01  *w                       if w < 8,
			1.17  + 0.0075*w - 0.00045*(w-8)^2     if w < 25,
			1.652 - 0.017 *w                       otherwise
		} * 1e-3 * stab_modify(wind, stability)
	}
}

library("Seawater") {
"""
This is a library for highly accurate, but more computationally expensive, properties of sea water (taking salinity into account). Se the library [Water utils](https://nivanorge.github.io/Mobius2/existingmodels/autogen/stdlib.html#water-utils) for simplified freshwater versions of these functions.

The formulas for density are taken from the [Matlab seawater package](http://www.marine.csiro.au/~morgan/seawater).

The formulas for viscosity and diffusivity are taken from

Riley, J. P. & Skirrow, G. Chemical oceanography. 2 edn, Vol. 1 606 (Academic Press, 1975).
"""
	
	load("basic_math.txt", library("Response"))
	
	fr_t_s : constant("Ice formation temperature salinity dependence", [deg_c], 0.056)
	
	ice_formation_temperature   : function(S : []) {
		# Ice formation temperature as a function of salinity
		-S*fr_t_s
	}

	seawater_dens_standard_mean : function(T : [deg_c]) {
		a0 := 999.842594,
		a1 := 6.793952e-2,
		a2 := -9.095290e-3,
		a3 := 1.001685e-4,
		a4 := -1.120083e-6,
		a5 := 6.536332e-9,
		T68 := T * 1.00024 => [],
		(a0 + (a1 + (a2 + (a3 + (a4 + a5*T68)*T68)*T68)*T68)*T68) => [k g, m-3]
	}

	seawater_pot_dens : function(T : [deg_c], S : []) {
		b0 := 8.24493e-1,
		b1 := -4.0899e-3,
		b2 := 7.6438e-5,
		b3 := -8.2467e-7,
		b4 := 5.3875e-9,
		c0 := -5.72466e-3,
		c1 := 1.0227e-4,
		c2 := -1.6546e-6,
		d0 := 4.8314e-4,
		T68 := T * 1.00024 => [],
		seawater_dens_standard_mean(T) 
		   + ((b0 + (b1 + (b2 + (b3 + b4*T68)*T68)*T68)*T68)*S + (c0 + (c1 + c2*T68)*T68)*S*sqrt(S) + d0*S^2) => [k g, m-3]
	}
	
	dynamic_viscosity_fresh_water : function(T : [deg_c]) {
		# Formula from J.P. Riley, Chemical Oceanography, vol 4, 2nd edition. AP. 1975
		# Returns viscosity in [Pa, s]
		# It is also possible to add pressure corrections. Pressure is here assumed to be at 1atm.
		eta20 := 1.0020e-3[Pa, s],             # Dynamic viscosity at t=20, s=0
		tm20  := 20[deg_c]-T,
		lograt := (1.1709*tm20 - 0.001827[deg_c-1]*tm20^2) / (T + 89.93[deg_c]),
		eta20*10^lograt   #Dynamic viscosity of distilled water, corrected for temperature
	}
	
	dynamic_viscosity_sea_water : function(T : [deg_c], S : []) {
		# Same reference as dynamic_viscosity_fresh_water
		eta_t := dynamic_viscosity_fresh_water(T),
		a := lerp(T, 5[deg_c], 25[deg_c], 0.000366, 0.001403),
		b := lerp(T, 5[deg_c], 25[deg_c], 0.002756, 0.003416),
		cl := max(0, (S-0.03)/1.805),
		clv := seawater_pot_dens(T, S) * cl =>[],  # Volume chlorinity    #TODO: What is the correct density to use here? Could also take it as a function argument since it is usually computed anyway.
		eta_t * (1 + a*sqrt(clv) + b*clv)
	}
	
	diffusivity_in_water : function(ref_diff, ref_T : [deg_c], ref_S : [], T : [deg_c], S : []) {
		# Diffusivity of some substance in water.
		# ref_diff  - the diffusivity of the substance in water at temperature ref_T and salinity ref_S.
		ref_diff * (dynamic_viscosity_sea_water(ref_T, ref_S) / dynamic_viscosity_sea_water(T, S)) * ((T -> [K]) / (ref_T -> [K]))
	}
}

library("Sea oxygen") { # Should be renamed since it includes other gases
"""
This contains formulas for O₂, CO₂ and CH₄ saturation and surface exchange in sea water. Based on

R.F. Weiss, The solubility of nitrogen, oxygen and argon in water and seawater, Deep Sea Research and Oceanographic Abstracts, Volume 17, Issue 4, 1970, 721-735, [https://doi.org/10.1016/0011-7471(70)90037-9](https://doi.org/10.1016/0011-7471(70)90037-9).

The implementation is influenced by the one in [SELMA](https://github.com/fabm-model/fabm/tree/master/src/models/selma).

There are other undocumented sources. This should be updated soon.
"""

	load("physiochemistry.txt", library("Chemistry"))
	
	o2_saturation_concentration : function (T : [deg_c], S : []) {
		# Formula from Weiss 1970 (also in Selma).
		Tk := (T -> [K]) => [],
		logsat := - 135.90205
			+ 1.575701e5  / Tk
			- 6.642308e7  / Tk^2
			+ 1.243800e10 / Tk^3
			- 8.621949e11 / Tk^4
			- S*(0.017674 - 10.754/Tk + 2140.7/Tk^2),
		exp(logsat) => [m mol, m-3]
	}
	
	# TODO: We could just provide the Henry's constant in the library and let the caller do the rest?
	
	# Compute saturation concentration of CO₂ in water (in mg(C)/l)
	co2_saturation_concentration : function(T : [deg_c], co2_atm_ppm : [], air_pressure) {
		Tk := (T->[K]) =>[],
		log10Kh := 108.3865 + 0.01985076*Tk - 6919.53/Tk - 40.45154*log10(Tk) + 669365/(Tk^2), # Stumm and Morgan 1996
		Kh := (10^log10Kh) => [mol, l-1, bar-1],
		
		pCO2 := co2_atm_ppm*1e-6*air_pressure,
		pCO2*Kh*c_mol_mass -> [m g, l-1]
	}
	
	# Compute saturation concentration of CO₂ in water (in mg(CH₄)/l)
	ch4_saturation_concentration : function(T : [deg_c], ch4_atm_ppm : [], air_pressure) {
		Tk := (T->[K]) =>[],
		Kh := exp( (-365.183 + 18103.7/Tk + 49.7554*ln(Tk) - 0.000285033*Tk)/1.9872)/55.556 => [mol, l-1, bar-1], # Source?
		
		pCH4 := ch4_atm_ppm*1e-6*air_pressure,
		pCH4*Kh*ch4_mol_mass -> [m g, l-1]
	}
	
	# TODO: Document the source of the Schmidt number calculations! Are they all from the same paper?
	
	schmidt_600 : function(wind : [m, s-1]) {
		wnd := wind => [],
		2.07 + 0.215*(wnd^1.7)
	}

	o2_piston_velocity : function(wind : [m, s-1], temp : [deg_c]) {
		T       := temp => [],
		k_600   := schmidt_600(wind),
		schmidt := 1800.6 - 120.10*T + 3.7818*T^2 - 0.047608*T^3,
		k_600*(schmidt/600)^(-0.666) => [c m, hr-1]
	}
	
	co2_piston_velocity : function(wind : [m, s-1], temp : [deg_c]) {
		T       := temp => [],
		k_600   := schmidt_600(wind),
		schmidt := 1923.6 - 125.06*T + 4.3773*T^2 - 0.085681*T^3 + 0.00070284*T^4,
		k_600*min(2.5, (schmidt/600)^(-0.666)) => [c m, hr-1]
	}
	
	ch4_piston_velocity : function(wind : [m, s-1], temp : [deg_c]) {
		T       := temp => [],
		k_600   := schmidt_600(wind),
		schmidt := 1909.4 - 120.78*T + 4.1555*T^2 - 0.080578*T^3 + 0.00065777*T^4,
		k_600*min(2.5, (schmidt/600)^(-0.666)) => [c m, hr-1]
	}
}