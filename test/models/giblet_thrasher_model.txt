
model("Giblet-Thrasher test model") {
	
	habitat : compartment("Habitat", hi)
	pred : quantity("Thrashers")
	prey : quantity("Giblets")
	
	solve(solver("LV-Solver", inca_dascru, [2, hr]), habitat.pred, habitat.prey)
	
	hi : index_set("Habitat index")     #TODO: There is an error if this is inline-declared in the distribute() declaration.
	route : index_set("Migration route index") @sub(hi)
	
	mig : connection("Migration route connection") @directed_graph(route) { habitat* }
	
	migr : compartment("Migration route", hi, route)
	
	module("Giblet-Thrasher", version(1, 1, 0)) {
		
		load("stdlib/basic_math.txt", library("Response"))
		
		par_group("General parameters") {
			birth_time : par_int ("Giblet preferred birthday", [day], 120, 30, 335)
			pmig       : par_real("Giblet propensity to migrate", [day-1], 0.05, 0, 1)
			nomnom     : par_real("Predation rate", [day-1], 0.1)
			pred_birth : par_real("Thrasher birth rate", [day-1], 0.025)
			pred_death : par_real("Thrasher death rate", [day-1], 0.05)
			meteor_dth : par_real("Meteor strike death rate increase", [day-1], 0.7)
		}
		
		par_group("Habitat parameters", habitat) {
			init_pred  : par_real("Initial predators", [], 1)
			init_prey  : par_real("Initial prey", [], 1)
			prey_birth : par_real("Giblet birth rate", [day-1], 0.2)
		}
		
		par_group("Migration weights", migr) {
			mig_wt : par_real("Migration weight", [], 0.1, 0, 1)
		}
		
		meteor : property("Meteor strikes")
		
		var(habitat.meteor, [])
		
		var(habitat.pred, []) @initial { init_pred }
		var(habitat.prey, []) @initial { init_prey }
		
		flux(out, habitat.prey, [day-1], "Giblet birth")     {  prey * prey_birth * bump_response(time.day_of_year, birth_time-60.0[day], birth_time, birth_time+60.0[day], 0.6, 1, 0.6) }
		
		flux(habitat.prey, out, [day-1], "Predation")        {  nomnom * prey * pred ->> }
		
		flux(out, habitat.pred, [day-1], "Thrasher birth")   {  pred_birth * prey * pred }
		
		flux(habitat.pred, out, [day-1], "Thrasher death")   {  pred * (pred_death + meteor_dth*meteor) }
		
		flux(habitat.prey, mig, [day-1], "Giblet migration")     {  prey * pred * pmig * mig_wt  }
	}
}


