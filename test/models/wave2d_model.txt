

model("Wave2D") {
	
	module("Wave2D", 1, 0, 0) {
		
		cell : compartment("Cell")
		
		amp   : quantity("amplitude")
		mom   : quantity("momentum")
		
		cell.par_group("Layers") {
			in_amp  : par_real("Initial amplitude", [m], 0)
		}
		
		var(cell.amp, [m]) .initial { in_amp }
		var(cell.mom, [m, day-1])
		
		h : connection("Horizontal", grid1d) { cell* }
		v : connection("Vertical", grid1d) { cell* }
		
		c : constant("Wave speed", [day-1], 0.05)
		
		flux(cell.mom, h, [m, day-2], "Momentum propagation horizontal") {
			c^2*(amp[below] - amp)
		}
		
		flux(cell.mom, v, [m, day-2], "Momentum propagation vertical") {
			c^2*(amp[below] - amp)
		}
		
		flux(cell.amp, nowhere, [m, day-1], "Wave propagation") { mom }
		
	}

	
	ih : index_set("Horizontal")
	iv : index_set("Vertical")
	
	cell : compartment("Cell")
	
	amp   : quantity("amplitude")
	mom   : quantity("momentum")
	
	cell.distribute(iv, ih)
	
	sol : solver("Solver", "INCADascru", 0.1)
	sol.solve(cell.amp)
	sol.solve(cell.mom)
}