

model("Wave2D") {
	
	vi : index_set("Vertical")
	hi : index_set("Horizontal")
	
	cell : compartment("Cell", vi, hi)
	
	amp   : quantity("Amplitude")
	mom   : quantity("Momentum")
	
	solve(solver("Solver", inca_dascru, [hr]), cell.amp, cell.mom)
	
	v : connection("Vert") @grid1d(cell, vi)
	h : connection("Horz") @grid1d(cell, hi)
	
	module("Wave2D", version(1, 0, 0)) {
		
		par_group("Layers", cell) {
			in_amp  : par_real("Initial amplitude", [m], 0)
		}
		
		var(cell.amp, [m]) @initial { in_amp }
		var(cell.mom, [m, day-1])
		
		c : constant("Wave speed", [day-1], 0.08)
		
		flux(cell.mom, h, [m, day-2], "Momentum propagation horizontal") {  c^2*(amp[below] - amp)  } @bidirectional @no_store
		
		flux(cell.mom, v, [m, day-2], "Momentum propagation vertical") {  c^2*(amp[below] - amp)  } @bidirectional @no_store
		
		flux(cell.amp, out, [m, day-1], "Wave propagation") { mom } @no_store
	}
}