

model("Wave2D") {
	
	cell : compartment("Cell")
	
	amp   : quantity("Amplitude")
	mom   : quantity("Momentum")
	
	distribute(cell, index_set("Vertical"), index_set("Horizontal"))
	
	solve(solver("Solver", "INCADascru", 0.1), cell.amp, cell.mom)
	
	module("Wave2D", version(1, 0, 0)) {
		
		par_group("Layers", cell) {
			in_amp  : par_real("Initial amplitude", [m], 0)
		}
		
		var(cell.amp, [m]) @initial { in_amp }
		var(cell.mom, [m, day-1])
		
		h : connection("Horizontal", grid1d) { cell* }
		v : connection("Vertical", grid1d) { cell* }
		
		c : constant("Wave speed", [day-1], 0.08)
		
		flux(cell.mom, h, [m, day-2], "Momentum propagation horizontal") {  c^2*(amp[below] - amp)  }
		
		flux(cell.mom, v, [m, day-2], "Momentum propagation vertical") {  c^2*(amp[below] - amp)  }
		
		flux(cell.amp, nowhere, [m, day-1], "Wave propagation") { mom }
	}
}