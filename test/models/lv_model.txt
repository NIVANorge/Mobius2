
model("Predator-prey test model") {
	
	md : module("Predator-prey", 1, 1, 0) {
		
		habitat : compartment("Habitat")
		
		habitat.par_group("Habitat parameters") {
			init_pred : par_real("Initial predators", [], 1)
			init_prey : par_real("Initial prey", [], 1)
			
			prey_birth : par_real("Prey birth rate", [], 0.2)
			nomnom     : par_real("Predation rate", [], 0.1)
			pred_birth : par_real("Predator birth rate", [], 0.025)
			pred_death : par_real("Predator death rate", [], 0.05)
		}
		habitat.has(pred : quantity("Predators"), []) .initial { init_pred }
		habitat.has(prey : quantity("Prey"), [])      .initial { init_prey }
		
		flux(nowhere, habitat.prey, "Prey birth")     {  prey * prey_birth  }          # NOTE: we can omit writing habitat.prey since the compartment can be inferred from the flux declaration
		
		flux(habitat.prey, nowhere, "Predation")      {  nomnom * prey * pred  }
		
		flux(nowhere, habitat.pred, "Predator birth") {  pred_birth * prey * pred  }
		
		flux(habitat.pred, nowhere, "Predator death") {  pred * pred_death  }
	}
	
	habitat : compartment("Habitat")
	pred : quantity("Predators")
	prey : quantity("Prey")
	
	sol : solver("LV-Solver", "INCADascru", 0.1)
	sol.solve(habitat.prey)
	sol.solve(habitat.pred)
	
	hi : index_set("Habitat index")
	habitat.distribute(hi)
}