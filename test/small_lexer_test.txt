

module("HBVSnow lite", 1,0,0) {
	"A snow module"

	air        : compartment("Atmosphere")
	snow_box   : compartment("Snow box")
	
	snow_par :
	snow_box.par_group("Snow") {
		ddf_melt  : par_real("Degree-day factor for snowmelt", unit(m m, deg_c -1, day -1), 2.74, "Some description")
		t_snow    : par_real("Temperature at which precip falls as snow", unit(deg_c), 0)
		t_melt    : par_real("Temperature at which snow melts", unit(deg_c), 0)
		refreeze  : par_real("Refreeze efficiency", unit(), 0.5)
		snow_liq  : par_real("Liquid water fraction", unit(), 0.5)
		init_snow : par_real("Initial snow depth (water equivalents)", unit(), 0)
	}
	
	snow  : substance("Snow (water equivalents)")
	water : substance("Water")
	
	temp   : property("Temperature")
	precip : property("Precipitation")
	
	# Or should these two be external to the module?
	air.has(temp,   series("Air temperature", unit(deg_c)))
	air.has(precip, series("Precipitation", unit(mm, day -1)))
	
	snow_box.has(snow,  unit(m m)) .initial { init_snow }
	snow_box.has(water, unit(m m)) .initial { 0 }             # Not necessary to declare initial here, default is 0.
	
	
	
	# Source void means that we are not interested in tracking the state of the source, this is then a pure source to the system
	flux(void, snow_box.snow, "Precipitation falling as snow") {
		air.precip    if air.temp <= t_snow,
		0             otherwise
	}
	
	p_rain :
	flux(void, out, "Precipitation falling as rain") {       # target out means that this module does not know what the target is. The target has to be linked in the model composition later.
		air.precip    if air.temp > t_snow,
		0             otherwise
	}
	
	flux(snow_box.snow, snow_box.water, "Melt") {
		min(max(0, ddf_melt*(air.temp - t_melt)), last(snow_box.snow))
	}
	
	# It would be good if we don't have to declare min(..., last(snow_box.water)) here and instead have it automatic, but it would make order of evaluation of refreeze and melt_runoff ambiguous. Can we figure out a way to make this more clean?
	
	flux(snow_box.water, snow_box.snow, "Refreeze") {
		min(max(0, refreeze*ddf_melt*(t_melt - air.temp)), last(snow_box.water))
	}
	
	melt_runoff :
	flux(snow_box.water, out, "Melt runoff") {
		max(0, snow_box.water - snow_box.snow*snow_liq)
	}
}
