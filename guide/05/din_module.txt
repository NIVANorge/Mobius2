
module("DIN processes", version(0, 0, 1),
	air  : compartment,
	soil : compartment,
	gw   : compartment,
	river : compartment,
	water : quantity,
	in : quantity,
	temp : property
) {
	
	# Feel free to come up with more creative names for the
	# parameter groups
	par_group("DIN global") {
		din_in   : par_real("Non-agricultural DIN inputs", [k g, ha-1, year-1], 5)
		din_loss : par_real("DIN loss rate at 20°C", [day-1], 0.01)
		din_q10  : par_real("DIN loss response to 10°C change in temperature", [], 2)
	}
	
	# Fertilization is land-specific since you want to be able to turn it off for
	# non-agricultural land types
	par_group("Land specific DIN", soil) {
		init_din : par_real("Initial soil DIN concentration", [m g, l-1], 4)
		din_fert : par_real("Fertilizer IN addition", [k g, ha-1, day-1], 100)
		fert_day : par_int("Fertilizer addition day", [day], 150)
		diss     : par_real("Fertilizer N dissolution rate", [m m-1], 0.01)
	}
	
	var(soil.water.in, [k g, m-2], [m g, l-1], "Soil water DIN") @initial_conc { init_din }
	
	var(gw.water.in, [k g, m-2], [m g, l-1], "Groundwater DIN") @initial_conc { 0[m g, l-1] }
	
	var(river.water.in, [k g], [m g, l-1], "River water DIN") @initial_conc { 0[m g, l-1] }
	
	flux(out, soil.water.in, [k g, m-2, day-1], "Soil water DIN deposition and fixation") {
		din_in/365[day, year-1] ->>
	}
	
	flux(soil.water.in, out, [k g, m-2, day-1], "Soil water DIN loss") {
		in*din_loss*din_q10^((air.temp-20[deg_c])/10[deg_c])
	}
	
	var(soil.in, [k g, m-2], "Soil fertilizer N")
	
	flux(out, soil.in, [k g, m-2, day-1], "Soil fertilizer N addition") {
		din_fert->>     if time.day_of_year = fert_day,
		0               otherwise
	}
	
	flux(soil.in, soil.water.in, [k g, m-2, day-1], "Fertilizer N dissolution") {
		soil.in * diss * in_flux(water)
	}
}