

model("NIVAFjord-SimplyQ") {

	extend("nivafjord_model.txt")

	# NOTE: The extend() system is not quite powerful enough to just allow us to extend(simplyq_model.txt) here. Would prefer if we could fix this.
	
	snow_box : compartment("Snow layer")
	soil     : compartment("Soil")
	gw       : compartment("Groundwater")
	river    : compartment("River")
	#epi     : compartment("Epilimnion")
	#hyp     : compartment("Hypolimnion")
	
	snow     : quantity("Snow")
	
	flow     : property("Flow")
	pet      : property("Potential evapotranspiration")
	
	sc : index_set("Subcatchment")
	lu : index_set("Landscape units")
	
	distribute(soil,  sc, lu)
	distribute(gw,    sc)
	distribute(river, sc)
	
	par_group("Land cover", soil) {
		lu_prop  : par_real("Land use proportions", [], 1, 0, 1)
	}
	
	par_group("Catchment data", river) {
		a_catch  : par_real("Catchment area", [k m 2], 51.7, 1e-6, 7e6)
	}
	
	load("modules/pet.txt",
		module("Degree-day PET", air, soil, pet, temp))
	load("modules/hbv_snow.txt",
		module("HBVSnow", air, snow_box, snow, water, temp, precip, loc(soil.water)))
	load("modules/simplyq.txt",
		module("SimplyQ land", soil, gw, river, water, flow, pet, a_catch, loc(river.water), loc(river.water)),
		module("SimplyQ river", river, water, flow, loc(downstream)))
	load("modules/simplysoiltemp.txt",
		module("Simply soil temperature", air, soil, snow_box, snow, temp))
	load("modules/rivertemp.txt",
		module("RiverTemperature", air, soil, river, water, heat, temp))
	
	load("stdlib/physiochemistry.txt", library("Water utils"))
	
	aggregation_weight(soil, gw)    { lu_prop }
	aggregation_weight(soil, river) { lu_prop }
	
	unit_conversion(soil.water, river.water) { a_catch }
	unit_conversion(gw.water, river.water)   { a_catch }
	
	solve(sol, soil.water, gw.water, river.water)
	#solve(sol, bnd_layer.water) #TODO: Fix the bug and remove this.
	
	
	downstream : connection("Downstream") @directed_graph {
		river+
		#(river|epi)+
	} @no_cycles
	
	#unit_conversion(river.water, layer.water) { rho_water }  #NOTE: don't use river.water.rho here, because then we get the weird thing where it changes total mass depending on temperature.
	
	horz : connection("Fjord horizontal") @directed_graph(e_idx) { 
		river? layer+ bnd_layer?
	} @no_cycles                    # Not sure if we should have no_cycles, but it should always be acheivable since the direction doesn't matter.
	
	edge       : compartment("Basin edge")
	edge_layer : compartment("Basin edge layer")
	distribute(edge,       basin_idx, e_idx)
	distribute(edge_layer, basin_idx, e_idx, layer_idx)
	
	par_group("Fjord horizontal boundary", edge_layer) {
		w_bnd  : par_real("Boundary width", [m], 0, 0, 10000)
	}
	
	load("modules/nivafjord/horizontal_fluxes.txt", module("NIVAFjord horizontal fluxes", basin, layer, river, edge, edge_layer, water, salt, temp, salinity, flow, rho, pressure, dz, horz, vert, w_bnd))
	
	tracer : quantity("Tracer")
	load("modules/nivafjord/tracer_test.txt", module("NIVAFjord tracer test", layer, bnd_layer, river, water, tracer))
}

