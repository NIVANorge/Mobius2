
module("SimplySed", version(0, 6, 0),
	soil    : compartment,
	river   : compartment,
	water   : quantity,
	sed     : quantity,
	e_fact  : property,
	a_catch : par_real,
) {
"""
This is a simple sediment transport module created as a part of SimplyP.

version 0.6:
* First Mobius2 version.
* Dynamic vegetation cover is computed a bit differently.

New to version 0.5.1:
* Updated parameter doc strings

New to version 0.5:
* Replaced Q - SS input relationship aQ^b with (aQ)^b. Reduces strong correlation/covariance of a and b params.
* Moved reach slope to be a reach parameter.
* Remove need for “Arable” land class.
* Can have dynamic erodibility for all land classes and % spring-sown crops.
"""

	load("stdlib/basic_math.txt", library("Response"), library("Basic"))

	par_group("Soil erodibility", soil) {
		c_cov      : par_real("Vegetation cover factor", [], 0.2, 0, 1, "Vegetation cover factor, describing ratio between long-term erosion under the land use class, compared to under bare soil of the same soil type, slope, etc. Source from (R)USLE literature and area-weight as necessary to obtain a single value for the land class.")
		doy_spring : par_int ("Day of year when soil erodibility is max for spring-grown crops", [day], 60,  30, 335)
		doy_autumn : par_int ("Day of year when soil erodibility is max for autumn-grown crops", [day], 304, 30, 335)
		p_spring   : par_real("Proportion of spring-grown crops", [], 0.65, 0, 1)
		loadred    : par_real("Reduction of load in sediment", [], 0, 0, 1)
		shp        : par_enum("Cover factor shape", flat) { flat triangular smooth }
	}
	
	par_group("Land slope", soil) {
		land_slope : par_real("Mean slope of land", [deg], 4, 0, 90)
	}
	
	par_group("River erosion", river) {
		ksed  : par_real("Erosion scaling factor", [day, k m-1], 15, 0, 100)
		psed  : par_real("Erosion power factor", [], 2, 0.1, 5)
	}
	
	c_cover : property("Time dependent vegetation cover factor")
	
	var(river.water.sed, [k g], [m g, l-1], "Suspended sediments")

	var(soil.c_cover, []) {
		E_risk_period := 60.0[day],
		spring := {
			wedge_response(time.day_of_year, doy_spring - E_risk_period/2, doy_spring, doy_spring + E_risk_period/2, c_cov, 1, c_cov)    if shp.triangular,
			bump_response (time.day_of_year, doy_spring - E_risk_period/2, doy_spring, doy_spring + E_risk_period/2, c_cov, 1, c_cov)    if shp.smooth,
			c_cov                                                                                                                        otherwise
		},
		autumn := {
			wedge_response(time.day_of_year, doy_autumn - E_risk_period/2, doy_autumn, doy_autumn + E_risk_period/2, c_cov, 1, c_cov)    if shp.triangular,
			bump_response (time.day_of_year, doy_autumn - E_risk_period/2, doy_autumn, doy_autumn + E_risk_period/2, c_cov, 1, c_cov)    if shp.smooth,
			c_cov                                                                                                                        otherwise
		},
		spring*p_spring + autumn*(1 - p_spring)
	}
	
	#e_fact : property("Erosion factor")
	
	var(soil.e_fact, [k g, k m-2, day-1], "Erosion factor land")          {   land_slope * c_cover * (1 - loadred) =>>  }
	
	var(river.e_fact, [], "Erosion factor river")                         {   (1e-3*ksed * in_flux(water) / a_catch)^psed  }

	flux(nowhere, river.water.sed, [k g, day-1], "Sediment mobilization") {   a_catch * aggregate(soil.e_fact) * e_fact }
}


