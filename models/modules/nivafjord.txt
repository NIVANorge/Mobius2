
module("NIVAFjord", 0, 0, 1) {

	load("../../stdlib/physiochemistry.txt", library("Water utils"))

	layer : compartment("Fjord layer")
	
	water : quantity("Water")
	salt  : quantity("NaCl")
	heat  : quantity("Thermal energy")
	
	temp : property("Temperature")
	
	vert : connection("Fjord vertical", grid1d) { layer* }
	
	layer.par_group("Layer parameters") {
		A      : par_real("Layer area", [m 2], 10000, 0, 1e6)
		init_S : par_real("Initial salinity", [], 35.0, 20.0, 50.0)
		init_T : par_real("Initial temperature", [deg_c], 15.0, 0.1, 40.0)
	}
	
	layer.has(z : property("Thickness"), [m]) {
		1[m]   # Will be dynamic eventually
	} .initial { 1[m] }
	
	layer.has(water, [k g], "Layer water") .initial { A*z*rho_water }   #TODO: temperature adjust
	
	layer.water.has(salt, [k g], [g, k g-1], "Layer NaCl") .initial_conc { init_S => [g, k g-1] }
	
	layer.water.has(heat, [J], "Layer thermal energy") .initial_conc { C_water * (init_T->[K]) }
	
	
	layer.water.has(salinity : property("Salinity"), []) {
		(conc(salt)->[g, k g-1]) => []     # PSU ~= ppt = g/kg
	}
	
	layer.water.has(temp, [deg_c], "Layer temperature") {   #TODO!
		heat / (water*C_water) -> [deg_c]
	}
	
	Kdiff : property("Diffusion coefficient")
	
	layer.water.salt.has(Kdiff, [c m 2, s-1], "Diffusion coefficient (NaCl)") {    # Change units?
		ref_diff := 2.2e-5[c m 2, s-1]  # Reference diffusion coefficient at a given temperature and salinity. TODO: Should be checked
		#TODO: Should also be pressure dependent.
		diffusivity_in_water(ref_diff, 25.0[deg_c], 58.44, temp, salinity)   # Is it correct to use this salinity correction for salt diffusion, or should there be another formula?
	}
	
	layer.water.heat.has(Kdiff, [m 2, s-1], "Diffusion coefficient (heat)") {
		k := 0.6[W, m-1, K-1]  # Thermal conductivity. TODO: should itself depend on temperature, pressure and salinity!
		rho := water_density(temp)
		k / (rho * C_water)
	}
	
	flux(layer.water.salt, vert, "NaCl vertical diffusion") {
		Kd := 0.5*(Kdiff + target(Kdiff)) -> [m 2, day-1]
		dz := 0.5*(z + target(z))
		rho := water_density(temp)
		rho * A * Kd * ( (conc(salt) - target(conc(salt)))->[] ) / dz
	}
	
	flux(layer.water.heat, vert, "Thermal energy vertical diffusion") {
		Kd := 0.5*(Kdiff + target(Kdiff)) -> [m 2, day-1]
		dz := 0.5*(z + target(z))
		rho := water_density(temp)
		rho * A * Kd * (conc(heat) - target(conc(heat))) / dz
	}
	
	#layer.water.has(dynvisc : property("Dynamic viscosity"), [k Pa, s]) {
	#	dynamic_viscosity_water(temp, salinity)
	#}
}