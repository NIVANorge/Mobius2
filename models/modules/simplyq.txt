

module("SimplyQ land", version(0, 5, 0),
	soil  : compartment,
	water : quantity,
	flow  : property,
	pet   : property,
	gw_water : loc,
	runoff_target : loc,
	is_top_box : constant,
) {
"""
This is an adaption of a hydrology module originally implemented in Python as a part of the model SimplyP, which was published as

Jackson-Blake LA, Sample JE, Wade AJ, Helliwell RC, Skeffington RA. 2017. Are our dynamic water quality models too complex? A comparison of a new parsimonious phosphorus model, SimplyP, and INCA-P. Water Resources Research, 53, 5382â€“5399. [doi:10.1002/2016WR020132](https://doi.org/10.1002/2016WR020132)

New to version 0.5 :
- New implementation in the Mobius2 framework.

Authors: James E. Sample, Leah A. Jackson-Blake, Magnus D. Norling
"""
	load("stdlib/basic_math.txt", library("Response"))
	
	option(is_top_box) {
		par_group("Hydrology general", soil) {
			bfi    : par_real("Baseflow index", [],                        0.6,  0,    1, "Proportion of runoff that goes to the groundwater")
			qqinfl : par_real("Quick flow inflection point", [m m, day-1], 160, 20,  2000)
		}
	} @otherwise {
		par_group("Hydrology general", soil) {
			bfi    : par_real("Baseflow index", [],                        0.6,  0,    1, "Proportion of runoff that goes to the groundwater")
		}
	}
	
	par_group("Hydrology land", soil) {
		fc     : par_real("Field capacity", [m m],                     120,  0, 1000, "The retention volume below which no runoff occurs.")
		tc_s   : par_real("Soil water time constant", [day],             2,  1,   40)
	}
	
	var(soil.water, [m m], "Soil water volume")  @initial { fc }
	
	option(is_top_box) {
		flux(soil.water, runoff_target, [m m, day-1], "Quick flow") {
			drylim := 0.9,		# Could be a parameter. Stops quick flow below drylim*fc
			q_in   := in_flux(water)->>,
			q_in * s_response(water, drylim*fc, fc, 0, 1) * atan(q_in/qqinfl)*2/pi
		}
		
		flux(soil.water, out, [m m, day-1], "Evapotranspiration") {
			s_response(water, 0.5*fc, fc, 0, pet) # smoothly turn the etp on when water level is between 1/2 field capacity and field capacity.
		} @no_carry   # Don't carry dissolved substances in the evapotranspiration
	}
	
	var(soil.water.flow, [m m, day -1], "Soil water flow") {
		rate := (water - fc)/tc_s,
		s_response(water, fc, 1.01*fc, 0, rate) # smoothly turn the flow rate on between water volumes of fc and 1.01*fc
	}
	
	flux(soil.water, runoff_target, [m m, day-1], "Soil runoff")            {   flow * (1 - bfi)   }
	
	flux(soil.water, gw_water, [m m, day-1], "Recharge")                    {   flow * bfi   }
}

module("SimplyQ groundwater", version(0, 5, 0),
	gw : compartment,
	river : compartment,
	water : quantity,
	flow : property,
	gw_target : loc,
	a_catch : par_real
) {
	par_group("Groundwater", gw) {
		tc_g    : par_real("Groundwater time constant", [day],          30,  1,  400)
			@formerly("SimplyQ land\Groundwater\Groundwater time constant")
		gw_ret  : par_real("Groundwater retention volume", [m m],        0,  1, 1e4)
			@formerly("SimplyQ land\Groundwater\Groundwater retention volume")
	}
	
	var(gw.water,   [m m], "Groundwater volume") @initial {
		gw_ret + (tc_g * river.water.flow / a_catch) -> [m m]  #TODO: Alternatively, access the init_flow parameter and cut the river.water.flow property entirely
	}
	
	flux(gw.water, gw_target, [m m, day-1], "Groundwater runoff")           {   max(0, water-gw_ret) / tc_g   }
}

module("SimplyQ river", version(0, 5, 0),
	river : compartment,
	water : quantity,
	flow  : property,
	river_target : loc
) {
"""
The river part of SimplyQ.

Authors: Leah A. Jackson-Blake, Magnus D. Norling
"""

	par_group("Reach parameters", river) {
		slope          : par_real("Reach slope", [],                             0.014, 0.00001,        3,
			"Roughly the altitude difference between the uppermost and lowermost points divided by the length")
		len            : par_real("Reach length", [m],                           10000,       0, 10000000)
		c_mann         : par_real("Manning's roughness coefficient", [s, m-1/3],  0.04,   0.012,      0.1, 
			"Default of 0.04 is for clean winding natural channels. See e.g. Chow 1959 for a table of values for other channel types")
		init_flow      : par_real("Initial reach flow", [m 3, s-1],                  1,       0,     100)
	}
	
	var(river.water, [m 3], "Reach water volume") @initial {
		q := init_flow => [],   # Force it as dimensionless since it doesn't make sense to take fractional powers of it otherwise
		depth := 0.349[m] * q^0.34,
		width := 2.71[m] * q^0.557,
		width * depth * len
	}
	
	# TODO: ideally we should here be able to cut the property "Flow", but unfortunately it is being referenced by the gw module...

	# NOTE: Empirical formula, so the units don't make sense, hence forced unit conversion.
	var(river.water.flow, [m 3, s-1], "Reach flow") {
		0.28[m 3, s-1] * (water * sqrt(slope) / (len * c_mann) =>[] )^1.5
		
		#0.386[m 3, s-1] * ((water / len =>[])^0.606)*((sqrt(slope)/c_mann =>[])^0.909)
	} @initial { init_flow }
	@no_store # It is identical to the Reach flow flux. We only have it as a separate variable for reference by the gw module
	
	flux(river.water, river_target, [m 3, s-1], "Reach flow flux")  {  flow ->>  }
}

module("SimplyQ river trapezoid", version(0, 1, 0),
	river : compartment,
	water : quantity,
	flow  : property,
	river_target : loc
) {
"""
Alternative version of the SimplyQ river module assuming an (isosceles) trapezoid cross-section of the river, where there is a flat river bed in the middle of the river with width $$w_b$$, and where the top width of ther river $$w_t$$ depends on the river volume and is larger than $$w_b$$.

The bank slope $$s_b$$ is at any time the depth $$d$$ of the river divided by the extrusion width $$(w_t - w_b)/2$$ of the sloped bank at one side of the river. In other words, it is the tangent of the angle the sloped bank makes against the horizontal plane. A higher $$s_b$$ means a steeper bank, with a rectangular cross-section being the limit as $$s_b$$ increases.

The cross-section $$A$$ is

$$
A = frac{V}{L} = \frac{d}{2}(w_t + w_b).
$$

where $$V$$ and $$L$ are the reach volume and length. Since $$w_b$$, $$s_b$$ and $$L$$ are constant, these equations can be solved so that you can compute $$d$$ and the hydraulic radius as a functions of $$V$$.

The reach discharge is then computed using the [Manning formula](https://en.wikipedia.org/wiki/Manning_formula).

Authors: Magnus D. Norling
"""

	load("stdlib/basic_math.txt", library("Response"))

	par_group("Reach parameters", river) {
		slope          : par_real("Reach slope", [],                             0.014, 0.00001,        3)
		sb             : par_real("Reach bank slope", [],                            1, 0.00001,       10)
		wb             : par_real("Reach bottom width", [m],                         2,     0.1,    10000)
		len            : par_real("Reach length", [m],                           10000,       0, 10000000)
		c_mann         : par_real("Manning's roughness coefficient", [s, m-1/3],  0.04,   0.012,      0.1, 
			"Default of 0.04 is for clean winding natural channels. See e.g. Chow 1959 for a table of values for other channel types")
		init_flow      : par_real("Initial reach flow", [m 3, s-1],                  1,       0,     100)
	}
	
	A    : property("Cross-section area")
	Rh   : property("Hydraulic radius")
	v    : property("Velocity")
	T    : property("Residence time")
	d    : property("Depth")
	
	var(river.A, [m 2]) { water/len } @no_store
	
	var(river.Rh, [m]) {
		wetted_perimeter := wb + 2*sqrt( ( (wb*sb)^2 + 4*A*sb ) / (1 + sb^2) ),
		A / wetted_perimeter
	}
	
	var(river.v, [m, s-1], "Reach velocity") {
		water.flow / A
	}
	
	var(river.T, [day], "Reach residence time") {
		water / water.flow ->>
	}
	
	var(river.d, [m], "Reach depth") {
		0.5*( -wb*sb + sqrt((wb*sb)^2 + 4*A*sb) )
	}
	
	var(river.water, [m 3], "Reach water volume") @initial {
		#This is not exactly correct, and so will not cause the right initial reach flow.. :( Assumes rectangular instead of trapezoid cross-section and uses depth instead of hydraulic radius. But will not be very far off, and it will correct itself quickly.
		len*(flow*c_mann*cbrt(wb^2)/sqrt(slope))^(3/5.)
	}

	var(river.water.flow, [m 3, s -1], "Reach flow")    {
		(water/len)*Rh^(2/3.)*sqrt(slope)/c_mann
	} @initial { init_flow } @no_store
	
	flux(river.water, river_target, [m 3, s-1], "Reach flow flux")     {  flow  }
}


module("SimplyQ river manual", version(0, 0, 1),
	river : compartment,
	water : quantity,
	flow  : property,
    tc_reach : property,
	river_target : loc
) {
"""
Alternative version of the SimplyQ river module using a user-supplied mean velocity

Authors: Leah A. Jackson-Blake
"""

	par_group("Reach parameters", river) {
    	length   : par_real("Reach length", [m], 10000, 50, 10000000)
		velocity : par_real("Mean velocity", [m, s-1], 0.3, 0.0001, 8, "Mean water velocity in the reach")
	}
    
    init_flow : constant("Initial reach flow", [m 3, s-1], 0)
	
	var(river.water, [m 3], "Reach water volume") @initial {
        q := init_flow => [],   # Force it as dimensionless since it doesn't make sense to take fractional powers of it otherwise
		depth := 0.349[m] * q^0.34,
		width := 2.71[m] * q^0.557,
		width * depth * length
	}

    var(river.water.tc_reach, [s], "Reach travel time") { length / velocity }
    
	var(river.water.flow, [m 3, s-1], "Reach flow") {
        water / tc_reach
	} @initial { init_flow } @no_store
	
	flux(river.water, river_target, [m 3, s-1], "Daily mean reach flow")  {  flow ->>  }
}