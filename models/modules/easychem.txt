module("EasyChem", version(0, 0, 2),
	air    : compartment,
	epi    : compartment,
	hyp    : compartment,
	
	water  : quantity,
	oc     : quantity,
	phyt   : quantity,
	o2     : quantity,
	din    : quantity,
	phos   : quantity,
	on     : quantity,
	ice    : quantity,
	sed    : quantity,
	
	temp   : property,
	sw     : property,
	wind   : property,
	indicator : property,
	tp     : property,
	
	toc    : property,
	sed_oc : par_real,
	
	area   : property
) {
"""
This is a simple lake biogeochemical model for CNP and O₂ made to fit with EasyLake

Many of the equations are taken from SelmaProtBas, but with simplifications https://github.com/jorritmesman/selmaprotbas/tree/main
"""

	load("stdlib/basic_math.txt", library("Response"))
	load("stdlib/seawater.txt", library("Sea oxygen"))
	load("stdlib/physiochemistry.txt", library("Chemistry"))

	p_vel     : property("O₂ piston velocity")
	light_lim : property("Light limitation")
	N_lim     : property("Nitrogen limitation")
	P_lim     : property("Phosphorus limitation")
	
	f_par     : constant("Fraction of PAR in SW radiation", [],           0.45)  # Photosynthetically available radiation
	
	
	par_group("Lake specific chemistry", epi) {
		init_O2      : par_real("Initial O₂ saturation", [], 1, 0, 1)
		init_c       : par_real("Initial lake DOC concentration", [m g, l-1], 10, 0, 1000)
		init_n       : par_real("Initial lake DIN concentration", [m g, l-1], 0, 0, 1000)
		init_p       : par_real("Initial lake TDP concentration", [m g, l-1], 0, 0, 1000)
		init_phyt    : par_real("Initial phytoplankton concentration", [m g, l-1], 0.5, 0.01, 1)
	}
	
	par_group("Oxygen", epi) {
		pvel_scaler  : par_real("Piston velocity scaler for O₂", [], 1, 0.5, 1.5) #TODO: Remove this.
		sod          : par_real("Background sediment O₂ demand", [g, m-2, day-1], 0.5, 0.1, 4)
	}
	
	par_group("Nutrients", epi) {
		respQ10      : par_real("Respiration Q10", [], 1.58, 0.25, 4, "Adjustment of rate with 10°C change in temperature")
		COS_resp     : par_real("Carbon Oxidation State adjustment coefficient", [], 3, 1, 10) # TODO: Remove this.
		Km_o2        : par_real("Half-saturation concentration O₂", [m mol, m-3], 1.23e-2, 0, 1e-1)
		K_OM         : par_real("Respiration rate", [year-1], 1, 0, 10)
		COS_autoch   : par_real("Carbon Oxidation State autoch DOC", [],  0.5,  0, 1)
	}
	
	#TODO: Could some of these be constants and not adjustable parameters? Otherwise there is a lot of covariance in the parameter set when calibrating N and P concs.
	par_group("Phytoplankton", epi) {
		phyt_eq_20    : par_real("Phyto equilibrium at 20°C and no limitations", [m g, l-1], 0.01, 0, 5)
		phyt_q10      : par_real("Temp adjustment of Phyto equilibrium", [], 2, 1, 4)
		phyt_turnover : par_real("Phytoplankton turnover rate", [day-1], 0.2, 0.05, 0.4)
		
		iopt       : par_real("Optimal PAR intensity", [W, m-2], 100, 50, 250)
		alpha      : par_real("Half-saturation for nutrient uptake", [m mol, m-3], 1.65625, 1, 10)
		phyt_nc    : par_real("N/C ratio in Phytoplankton", [], 0.1509, 0.1, 0.2)
		phyt_pc    : par_real("P/C ratio in Phytoplankton", [], 0.009434, 0.009, 0.01)
		chl_a_f    : par_real("Chl-a fraction", [perc], 2, 1.5, 3.7, "How large a fraction of the phytoplankton mass is chlorophyll a")
	}
	
	var(epi.water.o2, [k g], [m g, l-1], "Epilimnion O₂")  @initial_conc { o2_saturation(temp, 0)*init_O2*o2_mol_mass->> }
	var(hyp.water.o2, [k g], [m g, l-1], "Hypolimnion O₂") @initial_conc { o2_saturation(temp, 0)*init_O2*o2_mol_mass->> }  
	
	var(epi.water.p_vel, [c m, hr-1]) {
		pvel_scaler * o2_piston_velocity(air.wind, temp)
	}
	
	o2sat : property("O₂ saturation concentration")
	var(epi.water.o2sat, [m g, l-1]) {
		o2_saturation(temp, 0)*o2_mol_mass -> [m g, l-1]
	}
	
	flux(out, epi.water.o2, [k g, day-1], "O₂ gas exchange at surface") {
		(!ice.indicator) * p_vel*(o2sat - conc(o2))*area ->>
	}

	#TODO: factor out 0.05[m g, l-1] as a constant.
	flux(epi.water.o2, out, [k g, day-1], "O₂ sediment consumption (epi)") {
		# The interface area between sediment and epilimnion is the epi surface area minus the hyp surface area.
		sod*min(1, 1 - (0.05[m g, l-1] - conc(o2))/0.05[m g, l-1])*(area - hyp.area) ->>
	}

	flux(hyp.water.o2, out, [k g, day-1], "O₂ sediment consumption (hypo)") {
		sod*min(1, 1 - (0.05[m g, l-1] - conc(o2))/0.05[m g, l-1])*area ->>
	}	
	
	var(epi.water.oc, [k g], [m g, l-1], "Epilimnion DOC")  @initial_conc { init_c }
	var(hyp.water.oc, [k g], [m g, l-1], "Hypolimnion DOC") @initial_conc { init_c }
	
	#TODO (MNO): We should probably have a correct unit for these: At least document what they mean since they are not a real mass variable.
	var(epi.water.oc.o2, [k g], [], "Epilimnion DOC-O₂ content")  @initial_conc { 1 }
	var(hyp.water.oc.o2, [k g], [], "Hypolimnion DOC-O₂ content") @initial_conc { 1 }
	
	nosc : property("Carbon Oxidation State") {
		(conc(oc.o2) - 1)*4    # TODO: Where does the number come from?
	}
	
	var(epi.water.oc.nosc, [], "Epilimnion NOSC")
	var(hyp.water.oc.nosc, [], "Hypolimnion NOSC")
	
	resp : property("Bacterial respiration") {
		# Bacterial respiration is adjusted for Temp, O2 concentration and COS of the DOC
		rate := (K_OM / 365.25[day, year-1]) * COS_resp^oc.nosc,
		halfsat := Km_o2*o2_mol_mass->[m g, l-1],
		o2_adj := conc(o2) / (halfsat + conc(o2)),
		q10_adjust(rate*o2_adj, 20[deg_c], temp, respQ10)
	}
	
	var(epi.water.resp, [day-1], "Bacterial mineralization (epi)")
	var(hyp.water.resp, [day-1], "Bacterial mineralization (hypo)")
	
	flux(hyp.water.oc, out, [k g, day-1], "DOC bacterial mineralization (hypo)") { resp * oc }
	flux(epi.water.oc, out, [k g, day-1], "DOC bacterial mineralization (epi)")  { resp * oc }
	
	# note also that DOC to O2 ratio changes with COS of DOC, this is why we have "(2-(4+nosc)/4)"
	
	# TODO (MNO): from the formula for nosc, we have (2 - (4 + nosc)/4) = 2 - conc(oc.o2)  . Is there a reason not just to use that directly? Then you would not need nosc as a separate variable, it could just be computed for the use inside resp.
	flux(hyp.water.o2, out, [k g, day-1], "O₂ bacterial consumption (hypo)") { resp*o2*(o2_mol_mass/c_mol_mass)*(2 - (4 + oc.nosc)/4) }
	flux(epi.water.o2, out, [k g, day-1], "O₂ bacterial consumption (epi)")  { resp*o2*(o2_mol_mass/c_mol_mass)*(2 - (4 + oc.nosc)/4) } 
	
	chl_a : property("Chlorophyll-a")
	
	var(epi.water.phyt, [k g], [m g, l-1], "Epilimnion Phytoplankton")  @initial_conc { init_phyt }
	var(hyp.water.phyt, [k g], [m g, l-1], "Hypolimnion Phytoplankton") @initial_conc { init_phyt }
	
	var(epi.water.chl_a, [m g, l-1]) {
		conc(phyt)*chl_a_f->>
	}
	
	var(epi.water.phyt.light_lim, []) {
		# TODO: In Selma what we call iopt here is called imin, and the max(...) term is called iopt. Mislabeling here?
		# TODO: There should be some kind of self-shading where TOC and phyto remove shortwave.
		par_sw := sw*(1 - f_par),
		f := par_sw/max(0.5*par_sw, iopt),
		f*exp(1 - f)
	}
	
	var(epi.water.phyt.N_lim, []) {
		#NOTE: Use N molar mass instead of NO3 since that is what we track in the model (e.g. DIN is modeled as mg(N)/l)
		cmol := conc(water.din)/n_mol_mass->[m mol, m-3],
		ntemp := cmol^2,
		ntemp / ((alpha*phyt_nc)^2 + ntemp)
	}
	
	var(epi.water.phyt.P_lim, []) {
		#NOTE: Use P molar mass instead of PO4 since that is what we track in the model
		cmol := conc(water.phos)/p_mol_mass->[m mol, m-3],
		ptemp := cmol^2,
		ptemp / ((alpha*phyt_pc)^2 + ptemp)
	}
	
	equi : property("Equilibrium concentration")
	
	var(epi.water.phyt.equi, [m g, l-1], "Phytoplankton equilibrium concentration") {
		q10_adjust(phyt_eq_20, 20[deg_c], temp, phyt_q10)*min(light_lim, min(N_lim, P_lim))
	}
	
	fix : property("Photosynthetic C fixation")
	var(epi.water.phyt.fix, [k g, day-1]) {
		#Mgrowth := q10_adjust(phyt_rate, 20[deg_c], temp, phyt_q10),
		#min(light_lim, min(N_lim, P_lim))*Mgrowth*water->>
		phyt_turnover*(epi.water.phyt.equi*water->[k g] - phyt)
	}
	
	flux(out, epi.water.phyt, [k g, day-1], "Epilimnion photosynthetic C fixation") {   fix   }
	
	phyt_death_anox : constant("Phytoplankton increased death rate from anoxicity", [], 10)
	anox_threshold  : constant("Anoxicity threshold", [m g, l-1], 2)
	
	death : property("Phytoplankton death") {
		o2_factor := s_response(conc(o2), anox_threshold*0.9, anox_threshold*1.1, phyt_death_anox, 1),
		phyt_turnover*phyt*o2_factor
	}
	var(epi.water.phyt.death, [k g, day-1], "Phytoplankton death rate (epi)")
	var(hyp.water.phyt.death, [k g, day-1], "Phytoplankton death rate (epi)")
	
	flux(epi.water.phyt, epi.water.oc, [k g, day-1], "Phytoplankton death DOC release (epi)")  { phyt.death }
	flux(out, epi.water.oc.o2,         [k g, day-1], "Phytoplankton death O₂ release (epi)")   { phyt.death*(4 + COS_autoch)/4	}
	flux(hyp.water.phyt, hyp.water.oc, [k g, day-1], "Phytoplankton death DOC release (hypo)") { phyt.death	}
	flux(out, hyp.water.oc.o2,         [k g, day-1], "Phytoplankton death O₂ release (hypo)")  { phyt.death*(4 + COS_autoch)/4	}
	
	flux(out, epi.water.o2, [k g, day-1], "Epilimnion O₂ photosynthesis") {   phyt.fix*o2_mol_mass/c_mol_mass   }
	
	
	nc_molar_to_mass_ratio : function(nc_molar : []) {   nc_molar * n_mol_mass / c_mol_mass   }
	pc_molar_to_mass_ratio : function(pc_molar : []) {   pc_molar * p_mol_mass / c_mol_mass   }
	
	
	var(epi.water.din, [k g], [m g, l-1], "Epilimnion DIN")  @initial_conc { init_n }
	var(hyp.water.din, [k g], [m g, l-1], "Hypolimnion DIN") @initial_conc { init_n }
	
	flux(epi.water.din, out, [k g, day-1], "Phytoplankton N uptake") {   phyt.fix * nc_molar_to_mass_ratio(phyt_nc)   }
	
	var(epi.water.phos, [k g], [m g, l-1], "Epilimnion TDP")  @initial_conc { init_p }
	var(hyp.water.phos, [k g], [m g, l-1], "Hypolimnion TDP") @initial_conc { init_p }
	
	flux(epi.water.phos, out, [k g, day-1], "Phytoplankton P uptake") {   phyt.fix * pc_molar_to_mass_ratio(phyt_pc)   }
	#flux(out, epi.water.sed.phos, [k g, day-1], "Pytoplankton death P release (epi)") { phyt.death * pc_molar_to_mass_ratio(phyt_pc) }
	#flux(out, hyp.water.sed.phos, [k g, day-1], "Pytoplankton death P release (hyp)") { phyt.death * pc_molar_to_mass_ratio(phyt_pc) }
	
	flux(out, epi.water.phos, [k g, day-1], "Pytoplankton death P release (epi)") { phyt.death * pc_molar_to_mass_ratio(phyt_pc) }
	flux(out, hyp.water.phos, [k g, day-1], "Pytoplankton death P release (hyp)") { phyt.death * pc_molar_to_mass_ratio(phyt_pc) }
	
	
	tdpphyt : property("TDP + phyto P")
	
	var(epi.water.tdpphyt, [m g, l-1]) {
		conc(phos) + conc(phyt)*pc_molar_to_mass_ratio(phyt_pc)
	}
	
	var(epi.water.tp, [m g, l-1], "Epilimnion total phosphorous") {
		 conc(sed)*conc(sed.phos) + conc(phos) + conc(phyt)*pc_molar_to_mass_ratio(phyt_pc)
	}
	
	var(epi.water.on, [k g], [m g, l-1], "Epilimnion DON")
	var(hyp.water.on, [k g], [m g, l-1], "Hypolimnion DON")
	
	flux(out, epi.water.on, [k g, day-1], "Phytoplankton death N release (epi)") { phyt.death * nc_molar_to_mass_ratio(phyt_nc) }
	flux(out, hyp.water.on, [k g, day-1], "Phytoplankton death N release (hyp)") { phyt.death * nc_molar_to_mass_ratio(phyt_nc) }
	
	
	#TODO: The below is just experimentation. Clean it up.
	
	
	var(epi.water.toc, [m g, l-1], "Epilimnion TOC") {   conc(oc) + conc(phyt) + conc(sed)*sed_oc   }
}


module("EasyChem-Particulate", version(0, 0, 2),
	epi : compartment,
	hyp : compartment,
	#bot : compartment,
	water : quantity,
	sed   : quantity,
	phos  : quantity,
	area  : loc
) {
"""
Particle transport and settling for EasyChem.
"""
	
	par_group("Particles") {
		sett_vel : par_real("Sediment settling velocity", [m, day-1], 100, 50, 100, "This is the net settling velocity after taking into account upwards mixing whithin (but not between) each compartment.")
		#leech_r  : par_real("Sediment phosphorous internal loading rate", [k m-2, day-1], 0.001, 0, 1)
		sed_z    : par_real("Bottom sediment thickness", [m], 0.2, 0, 5, "Thickness of sediment layer participating in P exchange")
	}

	var(epi.water.sed, [k g], [m g, l-1], "Epilimnion suspended sediments")
	var(hyp.water.sed, [k g], [m g, l-1], "Hypolimnion suspended sediments")
	var(epi.water.sed.phos, [k g], "Epilimnion PP")
	@show_conc(epi.water, [m g, l-1])
	var(hyp.water.sed.phos, [k g], "Hypolimnion PP")
	@show_conc(hyp.water, [m g, l-1])
	
	flux(epi.water.sed, hyp.water.sed, [k g, day-1], "Epilimnion sediment settling") {
		sett_vel * conc(sed) * area ->>
	}
	
	#flux(hyp.water.sed, bot.sed, [k g, day-1], "Hypolimnion sediment settling") {
	flux(hyp.water.sed, out, [k g, day-1], "Hypolimnion sediment settling") {
		sett_vel * conc(sed) * area ->>
	}
	
	flux(hyp.water.sed.phos, out, [k g, day-1], "Hypolimnion PP settling") {
		sett_vel * conc(sed) * conc(sed.phos) * area ->>
	}
	
	sed_dens : constant("Sediment density", [k g, m-3], 2600)
	
	#var(bot.sed, [k g], "Bottom sediments") @initial { area*sed_z*sed_dens }
	#var(bot.sed.phos, [k g])
	
	# Not sure about this one, and if it should carry the P down:
	#flux(bot.sed, out, [k g, day-1], "Sediment burial") {
	#	sett_vel * conc(hyp.water.sed) * area ->>
	#}
	
	#flux(bot.sed.phos, hyp.water.phos, [k g, day-1], "Sediment P internal loading") {
	#	phos * area * leech_r ->>
	#}
	
	
	#TODO: There should be some interaction with the sediments where the nutrients that go there could leech out.
	# Maybe exchange between suspended particles and dissolved, but that may not be significant enough.
}


module("SuperEasyChem", version(0, 0, 1),
	epi : compartment,
	hyp : compartment,
	water : quantity,
	oc : quantity,
	din : quantity,
	phos : quantity,
	temp : property
) {	
"""
Simplified version of EasyChem where retention rates are given by empirical numbers instead of simulating phytoplankton or bacteria.
"""
	
	load("stdlib/basic_math.txt", library("Response"))
	
	par_group("Lake CNP", epi) {
		init_doc : par_real("Initial lake DOC concentration", [m g, l-1], 0, 0, 1000)
		init_din : par_real("Initial lake DIN concentration", [m g, l-1], 0, 0, 1000)
		init_tdp : par_real("Initial lake TDP concentration", [m g, l-1], 0, 0, 1000)

		lake_doc_ret : par_real("Lake DOC retention at 20°C", [day-1], 0, 0, 5)
		lake_doc_q10 : par_real("Lake DOC retention Q10", [], 1, 1, 5)
		lake_din_ret : par_real("Lake DIN retention at 20°C", [day-1], 0, 0, 5)
		lake_din_q10 : par_real("Lake DIN retention Q10", [], 1, 1, 5)
		lake_tdp_ret : par_real("Lake TDP retention at 20°C", [day-1], 0, 0, 5)
		lake_tdp_q10 : par_real("Lake TDP retention Q10", [], 1, 1, 5)

		hyp_ret : par_real("Hyplimnion relative retention", [], 0, 0, 1, "Factor used to reduce retention rates in the hypolimnion relatively to the epilimnion.")
	}
	
	var(epi.water.oc, [k g], [m g, l-1], "Epilimnion DOC")  @initial_conc { init_doc }
	var(hyp.water.oc, [k g], [m g, l-1], "Hypolimnion DOC") @initial_conc { init_doc }
	
	flux(epi.water.oc, out, [k g, day-1], "Epilimnion DOC retention") {
		ret := q10_adjust(lake_doc_ret, 20.0[deg_c], temp, lake_doc_q10),
		din * ret
	}
	
	flux(hyp.water.oc, out, [k g, day-1], "Hypolimnion DOC retention") {
		ret := q10_adjust(lake_doc_ret, 20.0[deg_c], temp, lake_doc_q10),
		din * ret * hyp_ret
	}
	
	var(epi.water.din, [k g], [m g, l-1], "Epilimnion DIN")  @initial_conc { init_din }
	var(hyp.water.din, [k g], [m g, l-1], "Hypolimnion DIN") @initial_conc { init_din }
	
	flux(epi.water.din, out, [k g, day-1], "Epilimnion N retention") {
		ret := q10_adjust(lake_din_ret, 20.0[deg_c], temp, lake_din_q10),
		din * ret
	}
	
	flux(hyp.water.din, out, [k g, day-1], "Hypolimnion N retention") {
		ret := q10_adjust(lake_din_ret, 20.0[deg_c], temp, lake_din_q10),
		din * ret * hyp_ret
	}
	
	var(epi.water.phos, [k g], [m g, l-1], "Epilimnion TDP")  @initial_conc { init_tdp }
	var(hyp.water.phos, [k g], [m g, l-1], "Hypolimnion TDP") @initial_conc { init_tdp }
	
	flux(epi.water.phos, out, [k g, day-1], "Epilimnion TDP retention") {
		ret := q10_adjust(lake_tdp_ret, 20.0[deg_c], temp, lake_tdp_q10),
		phos * ret
	}
	
	flux(hyp.water.phos, out, [k g, day-1], "Hypolimnion TDP retention") {
		ret := q10_adjust(lake_tdp_ret, 20.0[deg_c], temp, lake_tdp_q10),
		phos * ret * hyp_ret
	}
}