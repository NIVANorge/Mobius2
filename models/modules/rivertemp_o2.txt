

module("RiverTemperature", version(0, 1, 1),
	air   : compartment,
	soil  : compartment,
	river : compartment,
	water : quantity,
	heat  : quantity,
	temp  : property
) {
"""
A simple model for river water temperature. Water from the catchment is assumed to have the same temperature as the soil. Then heat is exchanged with the atmosphere with a constant rate relative to the temperature difference.

Authors: Magnus D. Norling
"""

	load("stdlib/physiochemistry.txt", library("Water utils"))
	
	min_t : constant("Minimum river temperature", [deg_c], 0.4)
	
	par_group("River temperature", river) {
		coeff : par_real("Air-river heat exchange coefficient", [day-1], 1, 0.1, 10)
	}
	
	var(river.water.heat, [J], "River thermal energy") @initial { water_temp_to_heat(water, max(air.temp, min_t)) }
	
	flux(out, river.water.heat, [J, day-1], "River heat from land") {
		#TODO: maybe modify if melt water    
		V := in_flux(water) -> [m 3, day-1],
		t := max(min_t, aggregate(soil.temp)),
		water_temp_to_heat(V => [m 3], t) =>> #Again annoying that the day-1 doesn't "pass through" the function.
	}
	
	flux(out, river.water.heat, [J, day-1], "River sensible heat flux") {
		(water_temp_to_heat(water, max(air.temp, min_t)) - heat)*coeff
	}
	
	var(river.water.temp, [deg_c], "River temperature") {
		water_heat_to_temp(water, water.heat)
	}
	
}

module("Simple River O₂", version(0, 1, 1),
	river : compartment,
	water : quantity,
	o2    : quantity,
	temp : property
) {
"""
The purpose of this module is only to provide oxygen inputs to EasyChem and NIVAFjord.

Authors: Magnus D. Norling
"""
	
	load("stdlib/seawater.txt", library("Sea oxygen"))
	load("stdlib/physiochemistry.txt", library("Chemistry"))
	
	par_group("River oxygen") {
		f_o2sat    : par_real("Catchment O₂ saturation fraction", [], 0.9, 0, 1)
	}

	var(river.water.o2, [k g], [m g, l-1], "River oxygen") 
	
	flux(out, river.water.o2, [k g, day-1], "River oxygen from catchment") {
		catch_conc := f_o2sat*o2_saturation_concentration(temp, 0) * o2_mol_mass,
		in_flux(water)*catch_conc->>
	}
}