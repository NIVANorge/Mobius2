
module("Degree-day PET", version(1, 0, 0),
	air  : compartment,
	soil : compartment,
	pet  : property,
	temp : property
) {
"""
This is a very simple potential evapotranspiration model that uses a linear relationship between PET and air temperature.

Authors: Magnus D. Norling
"""
	
	par_group("Potential evapotranspiration", soil) {
		ddf_pet   : par_real("Degree-day factor for evapotranspiration", [m m, deg_c -1, day -1], 0.12, 0.05, 0.2)
		pet_min_t : par_real("Minimal temperature for evapotranspiration", [deg_c], 0, -5, 5)
	}
	
	var(soil.pet, [m m, day -1]) {   max(0, ddf_pet*(air.temp - pet_min_t))   }
}


module("Penman-Monteith PET daily", version(1, 0, 0),
	air      : compartment,
	soil     : compartment,
	snow_box : compartment,
	snow     : quantity,
	temp     : property,
	pet      : property,
	pressure : property,
	lwd      : property,
	g_rad    : property,
	wind     : property,
	s_vap    : property,
	a_vap    : property
) {
"""
This implementation of the Penman-Monteith equation for potential evapotranspiration for the most part follows

Ventura, F., Spano, D., Duce, P. et al. An evaluation of common evapotranspiration equations. Irrig Sci 18, 163â€“170 (1999). [https://doi.org/10.1007/s002710050058](https://doi.org/10.1007/s002710050058)

See also [FAO paper 56](https://www.fao.org/4/X0490E/x0490e00.htm).

It is possible to configure it to instead use the Priestley-Taylor equation (i.e. replacing wind correction with a constant).

Authors: Magnus D. Norling
"""
	
	load("stdlib/atmospheric.txt", library("Meteorology"))
	load("stdlib/physiochemistry.txt", library("Thermodynamics"), library("Water utils"))
	
	par_group("Potential evapotranspiration", soil) {
		pt    : par_bool("Priestley-Taylor simplification", true, "Drops wind correction")
		apet  : par_real("PET multiplication factor", [], 1.28, 0.3, 4.0, "Only used with Priestley-Taylor")
		lalb  : par_real("Land albedo", [], 0.23, 0, 1)
		salb  : par_real("Snow albedo", [], 0.8,  0, 1)
		r_s   : par_real("Surface resistance", [s, m-1], 70, "Only used without Priestley-Taylor") #TODO: We could compute these based on other data like vegetation.
		r_a_w : par_real("Aerodynamic resistance / wind speed", [], 208, "Only used without Priestley-Taylor")
	}
	
	net_sw : property("Net shortwave")
	lwu    : property("Emitted longwave radiation")
	
	var(soil.net_sw, [W, m-2]) {
		alb := {
			lalb     if snow_box.snow < 0.5[m m],
			salb     otherwise
		},
		air.g_rad * (1 - alb)
	}

	var(soil.lwu, [W, m-2]) {
		emissivity := 0.9, #TODO: Could maybe be made more granular e.g. for snow
		emissivity*black_body_radiation(temp->[K])
	}
	
	# TODO: A potential improvement is to compute mean s_vap using min and max temperatures (if available). See the main reference above.
	# TODO: If an hourly version were to be made of this, one would need to compute the air-soil heat exchange and add it to the radiation term.
	
	var(soil.pet, [m m, day-1]) {
		
		# NOTE: we could factor some of these out as properties, which can be interesting to some for debugging
		lvap    := latent_heat_of_vaporization(air.temp),
		psy     := psychrometric_constant(air.pressure->[k Pa], lvap),
		slope   := slope_of_saturation_pressure_curve(air.temp, air.s_vap->[k Pa]),
		# May need to substitute  with the net longwave here: https://www.fao.org/3/x0490e/x0490e07.htm because this doesn't seem to give good results.
		# Also, the energy balance should take snow melt into account.
		net_rad := net_sw + air.lwd - lwu,
		
		w := air.wind * 0.75, # Convert from 10m to 2m altitude
		u := max(0.5[m, s-1], w),
		# Modified psychrometric constant
		psy_corr := {
			psy                             if pt,
			psy * (1 + r_s*u/r_a_w)         otherwise
		},
		dp := (air.s_vap - air.a_vap) -> [k Pa],
		pressure_term := {
			0                                                                      if pt,
			(lvap*vapor_mol_to_mass/(Rdry_air*r_a_w))*u*dp/(1.01*(air.temp->[K]))  otherwise
		} -> [W, m-2],
		alpha := {
			apet         if pt,
			1            otherwise
		},
	
		et := alpha * (slope*net_rad + psy*pressure_term) / (slope + psy_corr),
		
		max(0, et/(lvap*rho_water))->>
	}
}
