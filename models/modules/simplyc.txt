

module("SimplyC", 1, 0, 0) {
"""
Not finished, just for testing some systems.
"""

	load("stdlib/basic_math.txt", library("Rates"))

	air   : compartment("Atmosphere")
	soil  : compartment("Soil")
	gw    : compartment("Groundwater")
	river : compartment("River")

	water : quantity("Water")
	oc    : quantity("Organic carbon")
	
	air.has(so4 : property("SO4 deposition"), [m g, l-1])
	
	soil.par_group("DOC general") {
		cdoc       : par_real("Baseline soil DOC dissolution rate", [m g, l-1, day-1], 1, 0, 10)
		kt1        : par_real("Soil temperature DOC creation linear coefficient", [deg_c-1], 0, 0, 0.1)
		kt2        : par_real("Soil temperature DOC creation second-order coefficient", [deg_c-2], 0, 0, 0.1)
		kso4       : par_real("Soil DOC linear SO4 dependence", [l, m g-1], 0, 0, 0.1)
	}
	
	soil.par_group("DOC land") {
		basedoc    : par_real("Baseline soil DOC concentration", [m g, l-1], 10, 0, 100)
	}
	
	gw.par_group("DOC deep soil") {
		gwdoc_type : par_enum("Groundwater DOC computation type", soil_avg) { const soil_avg half_life }
		gwdochl    : par_real("Groundwater DOC half-life", [day], 80, 1, 500)
		gwdocconc  : par_real("Groundwater DOC concentration", [m g, l-1], 3, 0, 20)
	}
	
	temp : property("Temperature")
	
	soil.water.has (oc, [k g, k m -2], [m g, l-1], "Soil water DOC") .initial_conc { basedoc }
		
	gw.water.has   (oc, [k g, k m -2], [m g, l-1], "Deep soil DOC")  .initial_conc {
		gwdocconc                       if gwdoc_type.const,
		aggregate(conc(soil.water.oc))  otherwise
	} .override_conc {
		gwdocconc                       if gwdoc_type.const,
		aggregate(conc(soil.water.oc))  if gwdoc_type.soil_avg,
		no_override                     otherwise
	}
	
	river.water.has(oc, [k g], [m g, l-1], "River water DOC") .initial_conc { conc(gw.water.oc) }
	
	flux(nowhere, soil.water.oc, [k g, k m-2, day-1], "Soil DOC production") {  max(0, water*cdoc*(1 + (kt1 + kt2*temp)*temp - kso4*air.so4))  }
	
	smin :
	flux(soil.water.oc, nowhere, [k g, k m-2, day-1], "Soil DOC mineralization+resorption") {   oc * (cdoc / basedoc)    }
	
	gwmin :
	flux(gw.water.oc, nowhere, [k g, k m-2, day-1], "Deep soil DOC mineralization") {
		rate := hl_to_rate(gwdochl)
		gw.water.oc * rate
	}
}