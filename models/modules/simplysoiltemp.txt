
module("Simply soil temperature", version(0, 1, 0),
	air      : compartment,
	soil     : compartment,
	snow_box : compartment,
	snow     : quantity,
	temp     : property
) {
"""
This is a simplification of the soil temperature model developed for the INCA models.

Soil temperature is computed at a depth of 15 cm.

[Rankinen K. T. Karvonen and D. Butterfield (2004), A simple model for predicting soil temperature in snow covered and seasonally frozen soil; Model description and testing, Hydrol. Earth Syst. Sci., 8, 706-716](https://doi.org/10.5194/hess-8-706-2004)
"""
	coup     : property("COUP temperature")
	
	st_depth : constant("Soil temperature depth", [c m], 15)
	
	par_group("Soil temperature general") {
		depthst : par_real("Snow depth / soil temperature factor", [c m -1], -0.2, -3.0, -0.001)
		init_st : par_real("Initial soil temperature", [deg_c], 10, -30, 40)
	}
	
	par_group("Soil temperature land", soil) {
		stc     : par_real("Soil thermal conductivity / specific heat capacity", [m 2, M s -1], 0.4, 0.01, 0.8)
	}
	
	var(soil.coup, [deg_c]) @initial { init_st } {
		last(coup) + (stc  / (2*st_depth)^2) * (air.temp - last(coup))*time.step_length_in_seconds ->>
	}
	
	var(soil.temp, [deg_c], "Soil temperature") { 
		coup * exp(depthst * snow_box.snow ->[])
	} @initial { init_st }
}