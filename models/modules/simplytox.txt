


module("SimplyTox", 0, 0, 1) {
"""
Testing functionality for now.
"""
	load("../../stdlib/physiochemistry.txt", library("Thermodynamics"))

	air   : compartment("Atmosphere")
	soil  : compartment("Soil")
	dsoil : compartment("Slow-accessible soil")
	
	water : quantity("Water")
	oc    : quantity("Organic carbon")
	tox   : quantity("Toxin")
	
	temp  : property("Temperature")
	
	tox.par_group("Physiochemistry") {
		molmass     : par_real("Contaminant molar mass", unit(g, mol-1), 50, 0, 1000)
		molvol      : par_real("Contaminant molar volume", unit(c m 3, mol-1), 20, 0, 1000)
		dUaw        : par_real("Enthalpy of phase transfer between air and water", unit(k J, mol-1), 0, -100, 100)
		dUow        : par_real("Enthalpy of phase transfer between octanol and water", unit(k J, mol-1), 0, -100, 100)
		dUoa        : par_real("Enthalpy of phase transfer between octanol and air", unit(k J, mol-1), 0, -100, 100)
		kH25        : par_real("Henry's constant at 25°C", unit(Pa, m 3, mol-1), 0, 0, 100)  #TODO: better default?
		log10Kow25  : par_real("log10 Octanol-water partitioning coefficient at 25°C", unit(), 0, -10, 10)
		log10Koa25  : par_real("log10 Octanol-air partitioning coefficient at 25°C", unit(), 0, -10, 10)
		rwoc        : par_real("Water-OC rate coefficient", unit(),  0.5, 0, 3)  #TODO: unit
		rfs         : par_real("Fast-Slow rate coefficient", unit(), 0.05, 0, 3) #TODO: unit
	}
	
	soil.par_group("Soil carbon") {
		mocf        : par_real("Soil organic carbon, fast accessible", unit(k g, m-2), 30, 0, 300)
		mocs        : par_real("Soil organic carbon, slow accessible", unit(k g, m-2), 3, 0, 300)
	}
	
	#TODO: actually have to be able to do something like this:
	#  soil.tox.par_group()
	# but that syntax could be confusing since soil.tox is not a valid location.
	
	soil.has(oc, unit(k g, m-2))  .initial { mocf }
	dsoil.has(oc, unit(k g, m-2)) .initial { mocs }
	
	soil.water.has(tox, unit(n g, m-2), "Soil water toxin")
	soil.oc.has(tox, unit(n g, m-2),    "Fast-accessible SOC toxin")
	dsoil.oc.has(tox, unit(n g, m-2),   "Slow-accessible SOC toxin")
	soil.water.oc.has(tox, unit(n g, m-2), "Soil DOC toxin")
	
	air.has(prconc : property("Contaminant conc in precip"), unit(n g, m-3))
	precip : property("Precipitation")
	
	flux(nowhere, soil.water.tox, "Contaminant wet deposition") {
		air.precip * air.prconc * 1000    # NOTE: convert mm to m.
	}
	
	Kow   : property("Octanol-water partitioning coefficient") {
		log10Kow := enthalpy_adjust_log10(log10Kow25, deg_c_to_K(25), deg_c_to_K(temp), dUow)
		10^log10Kow
	}
	
	Koc   : property("SOC-water partitioning coefficient") {
		rhoSOC := 1900.0 # kg/m^3
		rOC    := 0.41   # Empirical constant.
		Kow * rOC / rhoSOC
	}
	
	Kdoc  : property("DOC-water partitioning coefficient") {
		rhoDOC := 1100.0  # kg/m^3
		(Kow - 0.45)^0.93 / rhoDOC
	}
	
	soil.water.tox.has(Kow, unit())
	soil.water.tox.has(Koc, unit(m 3, k g-1))
	soil.water.tox.has(Kdoc, unit(m 3, k g-1))
	
	flux(soil.water.tox, soil.oc.tox, "Water-SOC contaminant exchange") {
		rwoc*(conc(water.tox)*Koc - conc(oc.tox))
	}
	
	flux(soil.water.tox, soil.water.oc.tox, "Water-DOC contaminant exchange") {
		rwoc*(conc(water.tox)*Kdoc - conc(water.oc.tox)*1e-6)         #NOTE: for now 1e-6 is needed since DOC is in kg/km2
	}
	
	flux(soil.oc.tox, dsoil.oc.tox, "Fast-Slow contaminant exchange") {
		rfs*(conc(soil.oc.tox) - conc(dsoil.oc.tox))
	}
	
	river : compartment("River")
	
	river.water.has(tox, unit(k g))
	river.water.oc.has(tox, unit(k g))

}



