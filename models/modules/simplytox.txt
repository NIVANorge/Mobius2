

preamble("SimplyTox physiochemical", version(0, 0, 3),
	tox  : quantity,
	temp : property
) {

	load("stdlib/physiochemistry.txt", library("Thermodynamics"))
	load("stdlib/basic_math.txt", library("Response"))

	par_group("Physiochemistry", tox) {
		#molmass     : par_real("Contaminant molar mass", [g, mol-1], 50, 0, 1000)
		molvol      : par_real("Contaminant molar volume", [c m 3, mol-1], 20, 0, 1000)
		dUaw        : par_real("Enthalpy of phase transfer between air and water", [k J, mol-1], 0, -100, 100)
		dUow        : par_real("Enthalpy of phase transfer between octanol and water", [k J, mol-1], 0, -100, 100)
		#dUoa        : par_real("Enthalpy of phase transfer between octanol and air", [k J, mol-1], 0, -100, 100)
		kH25        : par_real("Henry's constant at 25째C", [Pa, m 3, mol-1], 0, 0, 100)  #TODO: better default?
		log10Kow25  : par_real("log10 Octanol-water partitioning coefficient at 25째C", [], 0, -10, 10)
		#log10Koa25  : par_real("log10 Octanol-air partitioning coefficient at 25째C", [], 0, -10, 10)
		rwoc        : par_real("Water-OC rate coefficient", [day-1],  0.5, 0, 3)
		rfs         : par_real("Fast-Slow rate coefficient", [day-1], 0.05, 0, 3)
		raw         : par_real("Water-air rate coefficient", [m, day-1], 0.05, 0, 3)
		raa         : par_real("Soil atmosphere exchange rate", [m, day-1], 0.05, 0, 10)
		
		hlfast      : par_real("Contaminant half life", [day], 2000, 0.1, 2e10)
		hlslow      : par_real("Contaminant half life (slow accessible)", [day], 20000, 0.1, 2e10)
		hlgw        : par_real("Contaminant half life (groundwater)", [day], 20000, 0.1, 2e10)
		degrq10     : par_real("Contaminant degradation Q10", [], 1, 1, 5, "Change in the rate with a change of 10째C change in temperature")
		degrref     : par_real("Temperature at which the half life is given.", [deg_c], 25, 0, 40)
	}
	
	Kow   : property("Octanol-water partitioning coefficient") {
		log10Kow := enthalpy_adjust_log10(log10Kow25, 25[deg_c]->[K], temp->[K], dUow),
		10^log10Kow
	}
	
	Koc   : property("SOC-water partitioning coefficient") {
		rhoSOC := 1900.0[k g, m-3],
		rOC    := 0.41,   # Empirical constant.
		Kow * rOC / rhoSOC
	}
	
	Kdoc  : property("DOC-water partitioning coefficient") {
		rhoDOC := 1100.0[k g, m-3],
		10.0^(-0.45)*Kow^0.93 / rhoDOC
	}
	
	Kh    : property("Henry's constant") {
		logH := enthalpy_adjust_log10(log10(kH25=>[]), 25[deg_c]->[K], temp->[K], dUaw),
		10^logH => [Pa, m 3, mol-1]
	}
	
	Kaw   : property("Air-water partitioning coefficient") {
		Kh / (temp->[K] * ideal_gas)
	}
	
	degr : function(hl : [day], q10 : [], reftemp : [deg_c], temp : [deg_c]) {
		q10_adjust(hl_to_rate(hl), reftemp, temp, q10)
	}
	
	Cw      : property("Equilibrium concentration")
	dep     : property("Contaminant deposition")
	atmconc : property("Atmospheric concentration")
}


module("SimplyTox soil", version(0, 0, 3),
	air   : compartment,
	soil  : compartment,
	dsoil : compartment,
	soilair : compartment,
	gw    : compartment,
	water : quantity,
	oc    : quantity,
	tox   : quantity,
	temp  : property,
	phys  : preamble
) {
"""
Testing functionality for now.
"""
	
	par_group("Soil carbon and air", soil) {
		mocf        : par_real("Soil organic carbon, fast accessible", [k g, m-2], 30, 0, 300)
		mocs        : par_real("Soil organic carbon, slow accessible", [k g, m-2], 3, 0, 300)
		Vsa         : par_real("Soil air volume", [m m], 10, 0, 1000)
	}
	
	par_group("Groundwater temperature and carbon", gw) {
		deeptemp    : par_real("Deep soil temperature", [deg_c], 8, 0, 20)
		mocdeep     : par_real("Deep soil organic carbon", [k g, m-2], 3, 0, 300)
	}
	
	#TODO: Make a deposition module also (where it can compute deposition from air concentration if it is not given separately - see Mobius1->INCATox).
	
	var(soil.oc, [k g, m-2])  @initial { mocf }
	var(dsoil.oc, [k g, m-2]) @initial { mocs }
	
	var(soil.water.tox, [n g, m-2], [n g, l-1], "Soil water dissolved contaminant")
	var(soil.oc.tox, [n g, m-2], [n g, k g-1], "Fast-accessible SOC contaminant")
	var(dsoil.oc.tox, [n g, m-2], [n g, k g-1], "Slow-accessible SOC contaminant")
	var(soil.water.oc.tox, [n g, m-2], [n g, k g-1], "Soil DOC contaminant")
	var(soilair.tox, [n g, m-2], "Soil air contaminant")

	
	var(air.dep, [n g, m-2, day-1])
	var(air.atmconc, [n g, m-3])
	
	flux(out, soil.water.tox, [n g, m-2, day-1], "Contaminant deposition to land") {   air.dep   }
	
	var(soil.water.tox.Kow, [])
	var(soil.water.tox.Koc, [m 3, k g-1])
	var(soil.water.tox.Kdoc, [m 3, k g-1])
	var(soil.water.tox.Kh, [Pa, m 3, mol-1])
	var(soil.water.tox.Kaw, [])

	var(soil.water.tox.Cw, [n g, l-1], "Soil water contaminant equilibrium concentration") {
		m_total := soil.water.tox + soil.oc.tox + soil.water.oc.tox + soilair.tox,
		m_total /
		(
			Koc * soil.oc -> [m m]
		  + Kdoc * soil.water.oc -> [m m]
		  + Kaw * Vsa
		  + water
		) ->>
	}
	
	var(soil.oc.tox.Cw, [n g, k g-1], "Soil SOC contaminant equilibrium concentration") { soil.water.tox.Cw * soil.water.tox.Koc ->> }
	var(soil.water.oc.tox.Cw, [n g, k g-1], "Soil DOC contaminant equilibrium concentration") { soil.water.tox.Cw * soil.water.tox.Kdoc ->> }
	var(soilair.tox.Cw, [n g, m-3], "Soil air contaminant equilibrium concentration") { soil.water.tox.Cw * soil.water.tox.Kaw ->> }
	
	flux(soil.water.tox, soil.oc.tox, [n g, m-2, day-1], "Water-SOC contaminant exchange") {
		rwoc*soil.oc*(soil.oc.tox.Cw - conc(soil.oc.tox))
	}
	
	flux(soil.water.tox, soil.water.oc.tox, [n g, m-2, day-1], "Water-DOC contaminant exchange (soil)") {
		rwoc*water.oc*(water.oc.tox.Cw - conc(water.oc.tox)) ->>
	}
	
	flux(soil.oc.tox, dsoil.oc.tox, [n g, m-2, day-1], "Fast-Slow contaminant exchange") {
		rfs*soil.oc*(conc(soil.oc.tox) - conc(dsoil.oc.tox)) ->>
	}
	
	flux(soil.water.tox, soilair.tox, [n g, m-2, day-1], "Water-air contaminant exchange") {
		concair := soilair.tox/Vsa -> [n g, m-3],
		raw*(soilair.tox.Cw - concair)
	}
	
	flux(soilair.tox, out, [n g, m-2, day-1], "Soil atmosphere contaminant exchange") {
		concair := soilair.tox/Vsa -> [n g, m-3],
		raa*(concair - air.atmconc)
	}
	
	flux(soil.water.tox, out, [n g, m-2, day-1], "Soil water dissolved contaminant decay") { degr(hlfast, degrq10, degrref, temp)*tox	}
	flux(soil.water.oc.tox, out, [n g, m-2, day-1], "Soil DOC contaminant decay") { degr(hlfast, degrq10, degrref, temp)*tox }
	flux(soil.oc.tox, out, [n g, m-2, day-1], "Fast-acessible SOC contaminant decay") { degr(hlfast, degrq10, degrref, temp)*tox }
	flux(dsoil.oc.tox, out, [n g, m-2, day-1], "Slow-acessible SOC contaminant decay") { degr(hlslow, degrq10, degrref, soil.temp)*tox }
	
	# Could instead have a heat conduction model, but maybe not necessary
	var(gw.temp, [deg_c], "Deep soil temperature") { deeptemp }
	var(gw.oc, [k g, m-2], "Deep soil carbon")  @initial { mocdeep }
	
	
	var(gw.oc.tox, [n g, m-2], [n g, k g-1], "Deep soil SOC contaminants")
	var(gw.water.tox, [n g, m-2], [n g, l-1], "Groundwater dissolved contaminants")
	var(gw.water.oc.tox, [n g, m-2], [n g, k g-1], "Groundwater DOC contaminants")
	
	var(gw.water.tox.Kow, [])
	var(gw.water.tox.Koc, [m 3, k g-1])
	var(gw.water.tox.Kdoc, [m 3, k g-1])
	
	var(gw.water.tox.Cw, [n g, l-1], "Groundwater dissolved contaminant equilibrium concentration") {
		m_total := gw.water.tox + gw.water.oc.tox + gw.oc.tox,
		m_total /
		(
			Koc * gw.oc -> [m m]
		  + Kdoc * gw.water.oc -> [m m]
		  + water
		)
	}
	
	var(gw.oc.tox.Cw, [n g, k g-1], "Deep soil SOC contaminant equilibrium concentration") {   gw.water.tox.Cw * gw.water.tox.Koc ->>  }
	var(gw.water.oc.tox.Cw, [n g, k g-1], "Groundwater DOC contaminant equilibrium concentration") {   gw.water.tox.Cw * gw.water.tox.Kdoc ->>  }
	
	flux(gw.water.tox, gw.oc.tox, [n g, m-2, day-1], "Water-SOC contaminant exchange (groundwater)") {
		# Maybe this should be slower though. It is supposed to be slow-accessible carbon only.
		rwoc*gw.oc*(gw.oc.tox.Cw - conc(gw.oc.tox)) ->>
	}
	
	flux(gw.water.tox, gw.water.oc.tox, [n g, m-2, day-1], "Water-DOC contaminant exchange (groundwater)") {
		rwoc*water.oc*(water.oc.tox.Cw - conc(water.oc.tox)) ->>
	}
	
	flux(gw.oc.tox, out, [n g, m-2, day-1], "Deep soil SOC contaminant decay") { degr(hlslow, degrq10, degrref, temp)*tox }
	flux(gw.water.tox, out, [n g, m-2, day-1], "Groundwater water dissolved contaminant decay") { degr(hlgw, degrq10, degrref, temp)*tox }
	flux(gw.water.oc.tox, out, [n g, m-2, day-1], "Groundwater DOC contaminant decay") { degr(hlgw, degrq10, degrref, temp)*tox }
}
	
	
module("SimplyTox river", version(0, 0, 1),
	air   : compartment,
	soil  : compartment,
	river : compartment,
	water : quantity,
	oc    : quantity,
	sed   : quantity,
	tox   : quantity,
	e_fact : property,
	temp   : property,
	wind   : property,
	eff_a : par_real,
	phys  : preamble
) {

	var(river.water.tox, [n g], [n g, l-1], "River dissolved contaminant")
	var(river.water.oc.tox, [n g], [n g, k g-1], "River DOC contaminant")
	var(river.water.sed.oc.tox, [n g], [n g, k g-1], "River POC contaminant")
	
	var(river.water.tox.Kow, [])
	var(river.water.tox.Koc, [m 3, k g-1])
	var(river.water.tox.Kdoc, [m 3, k g-1])
	var(river.water.tox.Kh, [Pa, m 3, mol-1])
	var(river.water.tox.Kaw, [])
	
	flux(soil.oc.tox, river.water.sed.oc.tox, [n g, m-2, day-1], "Sediment mobilization tox") {
		soil.e_fact*river.e_fact*conc(river.water.sed.oc)*conc(soil.oc.tox)->>
	}
	
	var(river.water.tox.Cw, [n g, l-1], "River dissolved contamninant equilibrium concentration") {
		m_total := river.water.tox + river.water.oc.tox + river.water.sed.oc.tox,
		m_total /
		(
			Koc * river.water.sed.oc
		  + Kdoc * river.water.oc
		  + water
		) ->>
	}
	var(river.water.oc.tox.Cw, [n g, k g-1], "River DOC contaminant equilibrium concentration") {
		river.water.tox.Cw * river.water.tox.Kdoc ->>
	}
	var(river.water.sed.oc.tox.Cw, [n g, k g-1], "River POC contaminant equilibrium concentration") {
		river.water.tox.Cw * river.water.tox.Koc ->>
	}
	
	flux(river.water.tox, river.water.sed.oc.tox, [n g, day-1], "Water-POC contaminant exchange (river)") {
		rwoc*river.water.sed.oc*(river.water.sed.oc.tox.Cw - conc(river.water.sed.oc.tox))
	}
	flux(river.water.tox, river.water.oc.tox, [n g, day-1], "Water-DOC contaminant exchange (river)") {
		rwoc*river.water.oc*(river.water.oc.tox.Cw - conc(river.water.oc.tox))
	}
	
	flux(river.water.tox, out, [n g, day-1], "River water dissolved contaminant decay") { degr(hlfast, degrq10, degrref, temp)*tox }
	flux(river.water.oc.tox, out, [n g, day-1], "River DOC contaminant decay") { degr(hlfast, degrq10, degrref, temp)*tox }
	flux(river.water.sed.oc.tox, out, [n g, day-1], "River POC contaminant decay") { degr(hlfast, degrq10, degrref, temp)*tox }
	
	
	
	# TODO: Factor this out to standard lib.
	# TODO: Put back in some of the references.
	
	molvol_air : constan("Molecular volume of air at surface pressure", [c m 3, mol-1], 20.1)
	molmass_air := constant("Molecular mass of air", [g, mol-1], 28.97)
	molvol_h2o := constant("Molecular volume of H2O vapour at surface pressure", [c m 3, mol-1], 22.41)
	molmass_h2o := constant("Molecular mass of H2O", [g, mol-1], 18.0)
	
	dynamic_viscosity_water : function(T : [K]) {
		# Freshwater at surface pressure
		2646.8[g, m-1, s-1]*exp(-0.0268*T=>[])
	}
	
	kinematic_viscosity_water : function(T : [K]) {
		# Freshwater at surface pressure
		0.00285[m 2, s-1]*exp(-0.027*T=>[])
	}
	
	molecular_diffusivity_in_air : function(mol_vol : [c m 3, mol-1], mol_mass : [g, mol-1], T : [K]) {
		TT := T=>[],
		c0 := cbrt(molvol_air=>[]) + cbrt(mol_vol=>[]),
		c := sqrt(1.0/(molmass_air=>[]) + 1.0/(mol_mass=>[])) / (c0^2),
		1e-7*c*TT^1.75 => [m 2, s-1]
	}
	
	molecular_diffusivity_in_water : function(T : [K], mol_vol : [c m 3, mol-1]) {
		# Hayduk and Laudie relationship.
		dynvis := dynamic_viscosity_water(T)=>[],
		13.26e-9[m 2, s-1] / ((dynvis^1.14)*((molvol=>[])^0.589))
	}
	
	diffusion_velocity_of_vapour_in_air : function(wind : [m, s-1]) {
		0.002*air.wind + 0.003[m, s-1]
	}
	
	transfer_velocity_of_CO2_in_water : function(wind : [m, s-1]) {
		w := wind => [],
		{
			0.65e-3                if w < 4.4,
			(0.79*w - 2.68)*1e-3   if w < 13.0,
			(1.64*w - 13.69)*1e-3  otherwise
		} => [c m, s-1]
	}
	
	
	vel : property("Contaminant transfer velocity")
	vel_air_water : property("Contaminant air-water transfer velocity")
	
	var(river.water.tox.vel, [m, s-1], "River water contaminant transfer velocity") {
		
		# TODO: This is only the low-turbulence case. Reimplement other cases for higher turbulence?
		# Deacon (TODO: full reference)
		
		moldiff_water := molecular_diffusivity_in_water(temp->[K], molvol),
		kinvis := kinematic_viscosity_water(temp->[K]),   # Kinematic viscosity of water
		schmidt := kinvis / moldiff_water, # Schmidt number
		
		asc := {
			0.67 if air.wind < 4.2[m, s-1],
			0.5  otherwise
		},
		vCO2 := transfer_velocity_of_CO2_in_water(air.wind),
		
		((schmidt/600)^(-asc))*vCO2*0.01 ->>
	}
	
	#TODO: Would like an   air.tox.vel, but air.tox is not a variable... If we tie it to river.water.tox, it is not reusable from the lake..
	# Maybe we should make it so that for properties (not quantities) one can tie something to a non-existing location?
	# On the other hand it is not that costly to declare it as a variable, just a bit confusing.
	/*
	
	var(air.tox.vel, [m, s-1], "Air contaminant transfer velocity") {
		
		moldiff_air    := molecular_diffusivity_in_air(molvol, molmass, temp->[K]),
		moldiffH2O_air := molecular_diffusivity_in_air(molvol_h2o, molmass_h2o, temp->[K]),
		vel_H2O        := diffusion_velocity_of_vapour_in_air(wind),
		
		vel_H2O*(moldiff_air/moldifH2O_air)^0.67
	}
	*/
	
	var(river.water.tox.vel_air_water, [m, s-1], "River air-water contaminant transfer velocity") {
		inv := (1.0/vel + 1.0/(air.tox.vel*Kaw)),
		1.0 / inv
	}
	
	flux(river.water.tox, out, [n g, day-1], "River water atmosphere contaminant exchange") {
		vel_air_water * (conc(water.tox) - concair/Kaw) ->> #TODO Check correctness. Is it multiplied on left or divided on right??
	}
	
	
	# Deposition to river surface (and other direct inputs)
	# Remains to do: River sediment bed (+ exchanges between river bed and water)
}



