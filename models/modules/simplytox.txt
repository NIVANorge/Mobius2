


module("SimplyTox soil", version(0, 0, 3),
	air   : compartment,
	soil  : compartment,
	dsoil : compartment,
	gw    : compartment,
	water : quantity,
	oc    : quantity,
	tox   : quantity,
	temp  : property
) {
"""
Testing functionality for now.
"""
	load("stdlib/physiochemistry.txt", library("Thermodynamics"))
	load("stdlib/basic_math.txt", library("Response"))

	par_group("Physiochemistry", tox) {
		#molmass     : par_real("Contaminant molar mass", [g, mol-1], 50, 0, 1000)
		#molvol      : par_real("Contaminant molar volume", [c m 3, mol-1], 20, 0, 1000)
		dUaw        : par_real("Enthalpy of phase transfer between air and water", [k J, mol-1], 0, -100, 100)
		dUow        : par_real("Enthalpy of phase transfer between octanol and water", [k J, mol-1], 0, -100, 100)
		#dUoa        : par_real("Enthalpy of phase transfer between octanol and air", [k J, mol-1], 0, -100, 100)
		kH25        : par_real("Henry's constant at 25°C", [Pa, m 3, mol-1], 0, 0, 100)  #TODO: better default?
		log10Kow25  : par_real("log10 Octanol-water partitioning coefficient at 25°C", [], 0, -10, 10)
		#log10Koa25  : par_real("log10 Octanol-air partitioning coefficient at 25°C", [], 0, -10, 10)
		#rwoc        : par_real("Water-OC rate coefficient", [m, day-1],  0.5, 0, 3)
		rwoc        : par_real("Water-OC rate coefficient", [k g, m-2, day-1],  0.5, 0, 3)
		rfs         : par_real("Fast-Slow rate coefficient", [k g, m-2, day-1], 0.05, 0, 3)
		hlfast      : par_real("Contaminant half life", [day], 2000, 0.1, 2e10)
		hlslow      : par_real("Contaminant half life (slow accessible)", [day], 20000, 0.1, 2e10)
		hlgw        : par_real("Contaminant half life (groundwater)", [day], 20000, 0.1, 2e10)
		degrq10     : par_real("Contaminant degradation Q10", [], 1, 1, 5)
		degrref     : par_real("Temperature at which the half life is given.", [deg_c], 25, 0, 40)
	}
	
	par_group("Groundwater temperature") {
		deeptemp    : par_real("Deep soil temperature", [deg_c], 8, 0, 20)
	}
	
	par_group("Soil carbon", soil) {
		mocf        : par_real("Soil organic carbon, fast accessible", [k g, m-2], 30, 0, 300)
		mocs        : par_real("Soil organic carbon, slow accessible", [k g, m-2], 3, 0, 300)
	}
	
	var(soil.oc, [k g, m-2])  @initial { mocf }
	var(dsoil.oc, [k g, m-2]) @initial { mocs }
	
	var(soil.water.tox, [n g, m-2], [n g, l-1], "Soil water dissolved toxin")
	var(soil.oc.tox, [n g, m-2], [n g, k g-1], "Fast-accessible SOC toxin")
	var(dsoil.oc.tox, [n g, m-2], [n g, k g-1], "Slow-accessible SOC toxin")
	var(soil.water.oc.tox, [n g, m-2], [n g, k g-1], "Soil DOC toxin")
	
	dep     : property("Contaminant deposition")
	#atmconc : property("Atmospheric concentration")
	
	var(air.dep, [n g, m-2, day-1])
	
	flux(out, soil.water.tox, [n g, m-2, day-1], "Contaminant deposition to land") {   air.dep   }
	
	Kow   : property("Octanol-water partitioning coefficient") {
		log10Kow := enthalpy_adjust_log10(log10Kow25, 25[deg_c]->[K], temp->[K], dUow),
		10^log10Kow
	}
	
	Koc   : property("SOC-water partitioning coefficient") {
		rhoSOC := 1900.0[k g, m-3],
		rOC    := 0.41,   # Empirical constant.
		Kow * rOC / rhoSOC
	}
	
	Kdoc  : property("DOC-water partitioning coefficient") {
		rhoDOC := 1100.0[k g, m-3],
		10.0^(-0.45)*Kow^0.93 / rhoDOC
	}
	
	Kh    : property("Henry's constant") {
		logH := enthalpy_adjust_log10(log10(kH25=>[]), 25[deg_c]->[K], temp->[K], dUaw),
		10^logH => [Pa, m 3, mol-1]
	}
	
	Kaw   : property("Air-water partitioning coefficient") {
		Kh / (temp->[K] * ideal_gas)
	}
	
	var(soil.water.tox.Kow, [])
	var(soil.water.tox.Koc, [m 3, k g-1])
	var(soil.water.tox.Kdoc, [m 3, k g-1])
	var(soil.water.tox.Kh, [Pa, m 3, mol-1])
	var(soil.water.tox.Kaw, [])
	
	
	flux(soil.water.tox, soil.oc.tox, [n g, m-2, day-1], "Water-SOC contaminant exchange") {
		rwoc*((conc(water.tox)->[n g, m-3])*Koc - conc(soil.oc.tox))
	}
	
	flux(soil.water.tox, soil.water.oc.tox, [n g, m-2, day-1], "Water-DOC contaminant exchange (soil)") {
		rwoc*((conc(water.tox)->[n g, m-3])*Kdoc - conc(water.oc.tox))
	}
	
	flux(soil.oc.tox, dsoil.oc.tox, [n g, m-2, day-1], "Fast-Slow contaminant exchange") {
		rfs*(conc(soil.oc.tox) - conc(dsoil.oc.tox))
	}
	
	degr : function(hl : [day], q10 : [], reftemp : [deg_c], temp : [deg_c]) {
		q10_adjust(hl_to_rate(hl), reftemp, temp, q10)
	}
	
	flux(soil.water.tox, out, [n g, m-2, day-1], "Soil water dissolved toxin decay") { degr(hlfast, degrq10, degrref, temp)*tox	}
	flux(soil.water.oc.tox, out, [n g, m-2, day-1], "Soil DOC toxin decay") { degr(hlfast, degrq10, degrref, temp)*tox }
	flux(soil.oc.tox, out, [n g, m-2, day-1], "Fast-acessible SOC toxin decay") { degr(hlfast, degrq10, degrref, temp)*tox }
	flux(dsoil.oc.tox, out, [n g, m-2, day-1], "Slow-acessible SOC toxin decay") { degr(hlslow, degrq10, degrref, soil.temp)*tox }
	
	
	var(gw.temp, [deg_c], "Groundwater temperature") {
		# Could instead have a heat conduction model, but maybe not necessary
		deeptemp
	}
	
	
	var(gw.water.tox, [n g, m-2], [n g, l-1], "Groundwater dissolved contaminants")
	var(gw.water.oc.tox, [n g, m-2], [n g, k g-1], "Groundwater DOC contaminants")
	var(gw.water.tox.Kow, [])
	var(gw.water.tox.Kdoc, [m 3, k g-1])
	
	flux(gw.water.tox, gw.water.oc.tox, [n g, m-2, day-1], "Water-DOC contaminant exchange (groundwater)") {
		rwoc*((conc(water.tox)->[n g, m-3])*Kdoc - conc(water.oc.tox))
	}
	
	
	# Could maybe have a separate groundwater rate
	flux(gw.water.tox, out, [n g, m-2, day-1], "Groundwater water dissolved toxin decay") { degr(hlgw, degrq10, degrref, temp)*tox }
	flux(gw.water.oc.tox, out, [n g, m-2, day-1], "Groundwater DOC toxin decay") { degr(hlgw, degrq10, degrref, temp)*tox }
}
	
	
module("SimplyTox river", version(0, 0, 1),
	gw    : compartment,
	river : compartment,
	water : quantity,
	oc    : quantity,
	sed   : quantity,
	tox   : quantity,
	Kow   : property,
	Koc   : property,
	Kdoc  : property,
	eff_a : par_real
) {

	#TODO: This module is now a mess after we factored out the above... Have to fix before use.



	par_group("River carbon", river) {
		sed_oc_c   : par_real("Sediment SOC concentration", [k g, k g-1], 0.02, 0, 0.3)
	}

	var(river.water.tox, [n g], [n g, l-1], "River dissolved toxin")
	var(river.water.oc.tox, [n g], [n g, k g-1], "River DOC toxin")
	
	var(river.water.sed.oc, [k g], [k g, k g-1], "River suspended sediment SOC") @override_conc { sed_oc_c }
	var(river.water.sed.oc.tox, [n g], [n g, k g-1], "River suspended sediment toxin")

	var(river.water.tox.Kow, [])
	var(river.water.tox.Koc, [m 3, k g-1])
	
	var(gw.water.tox.Kow, [])
	var(gw.water.tox.Kdoc, [m 3, k g-1])

	
	var(gw.temp, [deg_c]) {
		aggregate(soil.temp) #Should probably be modeled separately.
	}
	
	flux(gw.water.tox, gw.water.oc.tox, [n g, m-2, day-1], "Water-DOC contaminant exchange (groundwater)") {
		rwoc*((conc(water.tox)->[n g, m-3]) - conc(water.oc.tox)/Kdoc)
	}
	
	# These are very unstable, but because of the time scale they are probably not that important any way.
	#    It has to do with water.sed.oc becoming very small. But it is very sensitive to the threshold, so it is not that good a solution to check for it.
	
	#flux(river.water.tox, river.water.sed.oc.tox, [n g, day-1], "Water-SOC contaminant exchange (river)") {
	#	eff_a*rwoc*((conc(water.tox)->[n g, m-3]) - conc(water.sed.oc.tox)/Koc)    if water.sed.oc > 1e-6[k g, m-2]*eff_a,
	#	0                                                                          otherwise
	#}
	#flux(river.water.tox, river.water.oc.tox, [n g, day-1], "Water-DOC contaminant exchange (river)") {
	#	eff_a*rwoc*((conc(water.tox)->[n g, m-3]) - conc(water.oc.tox)/Kdoc)
	#}

	
	flux(soil.oc.tox, river.water.sed.oc.tox, [n g, m-2, day-1], "Sediment mobilization tox") {
		soil.e_fact*river.e_fact*sed_oc_c*conc(soil.oc.tox)->>
	}
	
	
	
	flux(river.water.tox, out, [n g, day-1], "River water dissolved toxin decay") { degr(hlfast, degrq10, degrref, temp)*tox }
	flux(river.water.oc.tox, out, [n g, day-1], "River DOC toxin decay") { degr(hlfast, degrq10, degrref, temp)*tox }
	flux(river.water.sed.oc.tox, out, [n g, day-1], "River suspended sediment toxin decay") { degr(hlfast, degrq10, degrref, temp)*tox }
	
	
	# Remains to do: River sediment bed (+ exchanges), air-water exchange (soil and river), deposition computation (from air conc)
	# Also slow SOC box in groundwater.
}



