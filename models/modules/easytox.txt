
module("EasyTox", version(0, 0, 0),
	air : compartment,
	epi : compartment,
	hyp : compartment,
	lake_sed : compartment,
	ice : quantity,
	water : quantity,
	oc  : quantity,
	tox : quantity,
	indicator : property,
	sw : property,
	temp : property,
	area : property,
	A_surf : loc,
	phys : preamble,
	sedpars : preamble
) {

	par_group("Photo-degradation", tox) {
		opt_c : par_real("Absorption cross-section", [m 2, mol-1], 2, "Absorption cross-section of contaminant")
		qy    : par_real("Quantum yield", [], 0.45, 0, 10, "Degraded contaminant mol per mol photons")
		f_par : par_real("Fraction of photo-active", [], 0.06, 0, 1, "Fraction of incoming shortwave radiation that contributes to degradation of this contaminant")
		e_par : par_real("Energy of photo-active", [J, mol-1], 351843, 0, 1000000, "Average energy per mol photons")
	}
	
	# Unfortunately we can't have this one in the air-water module, because we don't track river ice, so the same formula can't be reused for the river.
	flux(epi.water.tox, out, [n g, day-1], "Lake water-atmosphere contaminant exchange") {
		(!ice.indicator)*vel_air_water * (conc(water.tox)->[n g, m-3] - air.tox.atmconc/Kaw)* A_surf ->>
	}
	
	# TODO: Why does this slow down the model so much?
	flux(epi.water.tox, out, [n g, day-1], "Lake contaminant photo-degradation") {
		# TODO: What about attenuation of sw in the epilimnion?
		(!ice.indicator) * opt_c * qy * (f_par / e_par) * sw * tox ->>
	}
	
	var(hyp.water.tox.moldiff, [m 2, s-1])
	
	var(hyp.water.tox.diff_sed, [m 2, s-1]) {
		moldiff / (1 + 3*(1 + th))
	}
	
	var(lake_sed.temp, [deg_c], "Lake sediment temperature") { hyp.water.temp }
	
	#TODO: Hmm, should also ideally have exchange between sediments and epi, but there is an issue that the same sediment box connects to both hyp and epi, and that would
	# work as a conduit for transport from hyp to epi, which is not ideal.
	
	flux(hyp.water.tox, lake_sed.water.tox, [k g, day-1], "Lake-sediment bed diffusive dissolved contaminant exchange") {
		diff_sed * area * (conc(hyp.water.tox) - conc(lake_sed.water.tox)) / dz ->>
	}

	flux(hyp.water.oc.tox, lake_sed.water.oc.tox, [k g, day-1], "Lake-sediment bed diffusive DOC contaminant exchange") {
		hyp.water.tox.diff_sed * area * (conc(hyp.water.oc.tox)*conc(hyp.water.oc) - conc(lake_sed.water.oc.tox)*conc(lake_sed.water.oc)) / dz ->>
	}
	
	flux(out, river.water.tox, [n g, day-1], "Contaminant direct deposition") {
		air.tox.dep*A_surf ->>
	}
	
}