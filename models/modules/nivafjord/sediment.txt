
module("NIVAFjord sediments", version(0, 0, 3),
	layer    : compartment,
	sediment : compartment,
	water    : quantity,
	o2       : quantity,
	co2      : quantity,
	ch4      : quantity,
	salin    : property,
	sed      : quantity,
	oc       : quantity,
	din      : quantity,
	on       : quantity,
	phos     : quantity,
	op       : quantity,
	heat     : quantity,
	area     : property,
	temp     : property,
	dz       : property,
	z        : property,
	vert     : connection,
	dims     : preamble,
	chem_par : preamble
) {
	
	par_group("Sediments") {
		th       : par_real("Active sediment thickness", [m], 0.1, 0.00001, 1)
		porosity : par_real("Sediment porosity", [], 0.5, 0, 0.99)
		resus    : par_real("Resuspension fraction", [], 0.01, 0, 1, "Fraction of settling material that is resuspended")
		ch4_production_scaler : par_real("Sediment CH4 production scaler", [], 0.01, 0, 1, "sediment CH4 production relative to sediment CO2 production")
		bubble_ch4            : par_real("Bubble CH4 formation coefficient", [day-1], 0.5, 0, 1)
	}
	
	par_group("Initial chem", sediment) {
		init_c   : par_real("Initial sediment C fraction", [g, k g-1], 10, 0, 1)
		init_p   : par_real("Initial sediment IP fraction", [g, k g-1], 1, 0, 1)
		sed_doc  : par_real("Sediment pore water DOC concentration", [m g, l-1], 5, 0, 100)
	}
	
	load("stdlib/physiochemistry.txt", library("Chemistry"))
	
	# Set these as parameters: ?
	sed_rho : constant("Sediment density", [k g, m-3], 2600)
	sed_C   : constant("Sediment specific heat capacity", [J, k g-1, K-1], 4000) #TODO: Find a good value
	sed_k   : constant("Sediment heat conductivity coefficient", [W, m-1, K-1], 2) #TODO: This is 10 times too high, but otherwise heat just builds up...
	
	par_group("Sediment temperature") {
		dzh : par_real("Thickness of thermally active sediment layer", [m], 0.5)
		T_bot : par_real("Deep sediment temperature", [deg_c], 8)
	}
	
	var(sediment.dz, [m], "Active sediment thickness") { th } @no_store
	
	var(sediment.heat, [J], "Sediment thermal energy") @initial {
		(temp->[K]) * sed_C * (area*dzh) * sed_rho
	}
	
	var(sediment.temp, [deg_c], "Sediment surface temperature") {
		V := area*dzh,
		T_avg := heat / (sed_C*sed_rho*area*dzh) -> [deg_c],
		2*T_avg - T_bot
		#(T_avg - T_bot/4)*(4/3)
	} @initial { layer.water.temp } # Or T_bot?
	
	flux(layer.water.heat, sediment.heat, [J, day-1], "Water-sediment heat conduction") {
		dz_ := 0.5[m],#layer.dz,#0.5*(layer.dz + dzh),
		sediment.area*(layer.water.temp->[K] - sediment.temp->[K])*sed_k/dz_ ->>
	}
	
	
	sed_cn : constant("Sediment C/N", [], 6.25)
	sed_cp : constant("Sediment C/P", [], 106)
	
	
	# The sediment area of a water layer is the area of the layer minus the area of the water layer below.
	var(sediment.area, [m 2], "Sediment area") {   A - A[vert.below]   }
	
	var(sediment.sed, [k g], "Sediment mass") @initial {   area*th*sed_rho*(1 - porosity)   }
	
	var(sediment.sed.oc, [k g], [g, k g-1], "Sediment organic carbon") @initial_conc { init_c }
	
	var(sediment.sed.ch4, [k g], [g, k g-1], "Sediment CH4") 
	
	var(sediment.sed.on, [k g], [g, k g-1], "Sediment organic N") @initial_conc { init_c / cn_molar_to_mass_ratio(sed_cn) }
	
	var(sediment.sed.phos, [k g], [g, k g-1], "Sediment IP") @initial_conc { init_p }
	
	var(sediment.sed.op, [k g], [g, k g-1], "Sediment OP") @initial_conc { init_c / cp_molar_to_mass_ratio(sed_cp) }
	
	# The below is currently only used when we have contaminant partitioning for the sediments. Could also use the same system for other sediment interactions below...
	var(sediment.water, [m 3], "Sediment pore water") @override { area*th*porosity }
	var(sediment.water.oc, [k g], [m g, l-1], "Sediment pore water DOC") @override_conc { sed_doc }
	
	
	# NOTE: In NIVAFjord it is just a reduction in settling, here instead it causes mixing of concentrations. Not sure what is best.
	#   We could also make it independent of the settling (which is more realistic maybe).
	flux(sediment.sed, layer.water.sed, [k g, day-1], "Sediment resuspension") {  in_flux(sed)*resus ->>  }
	
	flux(sediment.sed, out, [k g, day-1], "Sediment burial") {   in_flux(sed)*(1 - resus) ->>   }  # Keep the layer the same thickness by moving down as much as accumulates on top
	
	
	#TODO: Take into account porosity?
	#  Should sediment processes access pore water instead, and then have slow exchange between pore water and water column?
	#           Probably unnecessary?
	
	#TODO: It is annoying to redo the below formulas for the sediments. Should be possible to unify the code.
	
	flux(sediment.sed.oc, out, [k g, day-1], "POC mineralization") {
		sed.oc*layer.water.min_rat
	} @no_carry
	
	flux(sediment.sed.on, layer.water.din, [k g, day-1], "PON mineralization and nitrification") {
		sed.on*layer.water.min_rat*n_min
	}
	
	# TODO: Should we assume PO4 formation is O2 limited?
	flux(sediment.sed.op, sediment.sed.phos, [k g, day-1], "POP mineralization") {
		sed.op*layer.water.temp_fac*maxdecomp_20*p_min
	}
	
	flux(layer.water.o2, out, [k g, day-1], "O₂ sediment microbial consumption") {
		mr := layer.water.min_rat,
		(sediment.sed.oc)*mr*(o2_mol_mass/c_mol_mass)*O2toC    
		+ (sediment.sed.on)*mr*n_min*(o2_mol_mass/n_mol_mass)*1.5    # ON -> NH4 -> NO3   #TODO: Should the 1.5 be parametrizable?
	}
	
	# Simple CO2 release from the sediment = O2 sediment demand
	flux(out, layer.water.co2, [k g, day-1], "CO₂ sediment microbial production") {
		mr := layer.water.min_rat,
		dr := layer.water.denit_rat,
		(sediment.sed.oc)*(mr + dr)    
	}
	
	# Simple CH4 production in the sediment: fraction scaled to CO2 production
	flux(out, sediment.sed.ch4, [k g, day-1], "CH4 sediment microbial production") {
		mr := layer.water.min_rat,
		(sediment.sed.oc) * mr * ch4_production_scaler    
	}
	
	ch4satconc : property("CH4 saturation concentration")
	
	## Simplified model of CH4 sollubility as a function of water temp, ambient pressure and salinity: Duan and Mao, 2006, https://doi.org/10.1016/j.gca.2006.03.018
	# TODO: Name the magic constants, and probably move to stdlib.
	var(sediment.sed.ch4satconc, [g, k g-1]) {
		ch4_mol_mass*((0.00247[mol, k g-1] - (0.00004033[mol, k g-1, deg_c-1] * sediment.temp))* (1-(0.2844-0.001775[deg_c-1]*sediment.temp) * (layer.water.salin/58.44)))* (1 + 44.6[m-1] * layer.z[vert.above]/490)
	}
	
	## Simple CH4 bubble release from the sediment 
	flux(sediment.sed.ch4, out, [k g, day-1], "CH4 sediment bubbling") {
		max(0, bubble_ch4*(conc(ch4) - ch4satconc)*sediment.sed) ->>
	}
	
	## Simple CH4 diffusion release from the sediment; Diffusion coefficient from 
	flux(sediment.sed.ch4, layer.water.ch4, [k g, day-1], "CH4 sediment diffusion release") {
		diff_coeff := (4.18e-11[m, m, s-1, deg_c-1]*sediment.temp+8.06e-10 [m, m, s-1]) * 86400[s, day-1],  ## m2 day-1
		diff_coeff  * (conc(sediment.sed.ch4)* 1000[g, l-1] *(porosity)- conc(layer.water.ch4)) * 0.001[k g,g-1] / 0.25 [m] * sediment.area
	}
	
	
	flux(sediment.sed.oc, out, [k g, day-1], "POC mineralization by denitrification") {
		sediment.sed.oc*layer.water.denit_rat
	} @no_carry
	
	flux(layer.water.din, out, [k g, day-1], "NO3 sediment denitrification consumption") {
		bo2 := sediment.sed.oc*layer.water.denit_rat*O2toC*o2_mol_mass/c_mol_mass,  # Bacterial O2 demand in nitrification
		bo2*1.5*n_mol_mass/o2_mol_mass
	}
	
	flux(layer.water.phos, sediment.sed.phos, [k g, day-1], "PO4 adsorption / desorption to sediment layer") {
		
		epc0 := conc(sediment.sed.phos)/k_sorp -> [m g, l-1],
		epc0_ := min(epc0, 0[m g, l-1]), # We get numeric instability if we don't put the 'min(.., 0)' there.
		
		# If there is high oxicity, use regular partitioning. With low oxicity, PO4 leeches out. (TODO: should maybe be a different rate)
		r_sorp*(ox_fac*(conc(water.phos) - epc0_)*water->[k g] - (1-ox_fac)*sediment.sed.phos)
	}
	
}