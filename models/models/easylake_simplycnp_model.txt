
model("EasyLake-SimplyCNP") {
	extend("easylake_simplyq_model.txt")
	extend("simplyc_model.txt")
	extend("simplyn_model.txt")
	extend("simplyp_model.txt")
	
	module("EasyLake-C", version(0, 0, 1)) {
		
		sw_rad : property("Shortwave radiation")
		
		par_group("Lake C", epi) {
			init_c : par_real("Initial lake DOC concentration", [m g, l-1], 0, 0, 1000)
			oc_doc : par_real("Optical cross-section of DOC", [m 2, m g-1], 0.01, 0.001, 1.0, "Can be used to scale the photomineralization speed")
		}
		
		var(epi.water.oc, [k g], [m g, l-1], "Epilimnion DOC")  @initial_conc { init_c }
		var(hyp.water.oc, [k g], [m g, l-1], "Hypolimnion DOC") @initial_conc { init_c }
		
		flux(epi.water.oc, nowhere, [k g, day-1], "Photo-mineralization") {
			
			#TODO: Maybe scale photomin differently than using the oc_doc
			
			qy_doc := 0.1[m g, mol-1],    # Quantum yield
			f_par  := 0.45,               # Fract. of PAR in incoming solar radiation
			e_par  := 240800.0[J, mol-1], # Average energy of PAR photons
			
			# TODO: This should be computed, and actually depends on the DOC conc. Moreover, the excess should go into the hypolimnion and cause photomineralization there.
			attn_epi := 1,
			
			oc_doc * qy_doc * (f_par / e_par) * sw_rad * attn_epi * oc ->>
		}
		
		# TODO: Some other sinks of DOC (otherwise hypolimnion conc is constant between mixing events, for instance.
	}
	
	module("EasyLake-N", version(0, 0, 1)) {
	
		load("stdlib/basic_math.txt", library("Rates"))

		par_group("Lake N", epi) {
			init_din : par_real("Initial lake DIN concentration", [m g, l-1], 0, 0, 1000)
			lake_din_ret : par_real("Lake DIN retention at 20Â°C", [day-1], 0, 0, 5)
			lake_din_q10 : par_real("Lake DIN retention Q10", [], 1, 1, 5)
			hyp_din      : par_real("Hypolimnion N retention factor", [], 1, 0, 2)
		}
		
		temp : property("Temperature")
		
		var(epi.water.din, [k g], [m g, l-1], "Epilimnion DIN")  @initial_conc { init_din }
		var(hyp.water.din, [k g], [m g, l-1], "Hypolimnion DIN") @initial_conc { init_din }
		
		flux(epi.water.din, nowhere, [k g, day-1], "Epilimnion N retention") {
			rate := q10_adjust(lake_din_ret, 20.0[deg_c], temp, lake_din_q10),
			din * rate
		}
		
		flux(hyp.water.din, nowhere, [k g, day-1], "Hypolimnion N retention") {
			rate := q10_adjust(lake_din_ret*hyp_din, 20.0[deg_c], temp, lake_din_q10),
			din * rate
		}
	}
	
	module("EasyLake-P", version(0, 0, 1)) {

		par_group("Lake P", epi) {
			init_tdp : par_real("Initial lake TDP concentration", [m g, l-1], 0, 0, 1000)
			tdp_ret : par_real("Lake TDP retention", [day-1], 0, 0, 5)
			hyp_tdp_ret : par_real("Hyplimnion TDP retention factor", [], 0, 0, 1)
		}
		
		var(epi.water.tdp, [k g], [m g, l-1], "Epilimnion TDP")  @initial_conc { init_tdp }
		var(hyp.water.tdp, [k g], [m g, l-1], "Hypolimnion TDP") @initial_conc { init_tdp }
		
		flux(epi.water.tdp, nowhere, [k g, day-1], "Epilimnion TDP retention") {
			tdp * tdp_ret
		}
		
		flux(hyp.water.tdp, nowhere, [k g, day-1], "Hypolimnion TDP retention") {
			tdp * tdp_ret * hyp_tdp_ret
		}
		
		# TODO: SS, PP
	}
}