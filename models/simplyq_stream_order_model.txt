
model("SimplyQ") {

	option_group("SimplyQ configuration") {
		pet_module : par_enum("PET module", degree_day) { degree_day penman_monteith }
	}


	sc       : index_set("Subcatchment")
	lu       : index_set("Landscape units")
	
	air      : compartment("Atmosphere",  sc)
	snow_box : compartment("Snow layer",  sc)
	soil     : compartment("Soil",        sc, lu)
	gw       : compartment("Groundwater", sc)
	river    : compartment("River",       sc)
	
	s1_2     : compartment("Stream order 1-2", sc)
	s3_4     : compartment("Stream order 3-4", sc)
	
	snow     : quantity("Snow")
	water    : quantity("Water")
	
	temp     : property("Temperature")
	precip   : property("Precipitation")
	flow     : property("Flow")
	pet      : property("Potential evapotranspiration")
	
	par_group("Land cover", soil) {
		lu_prop  : par_real("Land use proportions", [], 1, 0, 1)
	}
	
	assert("Land use proportions must sum to 1 in each subcatchment", river.water) {
		abs(aggregate(lu_prop) - 1) < 1e-8
	}
	
	par_group("Catchment data", river) {
		a_catch  : par_real("Catchment area", [k m 2], 51.7, 1e-6, 7e6)
		
		n1_2 : par_real("Number of 1-2nd order streams", [], 100)
		n3_4 : par_real("Number of 3-4th order streams", [], 20)
	}
	
	downstream : connection("Downstream")  @directed_graph { 
		river+ out
	} @no_cycles
	
	###### Import modules
	load("modules/hbv_snow.txt",  module("HBVSnow", air, snow_box, snow, water, temp, precip, loc(soil.water)))
	
	load("modules/simplyq.txt",
		module("SimplyQ land", soil, gw, river, water, flow, pet, a_catch, loc(s1_2.water), loc(s1_2.water)),
		module("SimplyQ river trapezoid", "1-2nd order streams", s1_2, water, flow, loc(s3_4.water)),
		module("SimplyQ river trapezoid", "3-4th order streams", s3_4, water, flow, loc(river.water)),
		module("SimplyQ river trapezoid", "Main river", river, water, flow, loc(downstream))
	)
	
	aggregation_weight(soil, gw)    { lu_prop }
	aggregation_weight(soil, s1_2)  { lu_prop }
	
	unit_conversion(soil.water, s1_2.water) { a_catch / n1_2 }
	unit_conversion(gw.water, s1_2.water)   { a_catch / n1_2 }
	
	unit_conversion(s1_2.water, s3_4.water) { n1_2 / n3_4 }
	unit_conversion(s3_4.water, river.water) { n3_4 }
	
	option(pet_module.degree_day) {
		load("modules/pet.txt",       module("Degree-day PET", air, soil, pet, temp))
	}
	option(pet_module.penman_monteith) {
		
		pressure : property("Pressure")
		lwd      : property("Downwelling longwave radiation")
		g_rad    : property("Global radiation")
		wind     : property("Wind speed")
		s_vap    : property("Saturation vapor pressure")
		a_vap    : property("Actual vapor pressure")
		cos_z    : property("Cosine of the solar zenith angle")
		dens     : property("Density")
		a_hum    : property("Actual specific humidity")
		
		load("modules/simplysoiltemp.txt",
			module("Simply soil temperature", air, soil, snow_box, snow, temp))
		
		load("modules/atmospheric.txt",
			module("Atmospheric", air, temp, wind, g_rad, pressure, a_hum, dens, lwd, cos_z, a_vap, s_vap))
		
		load("modules/pet.txt",
			module("Penman-Monteith PET daily", air, soil, snow_box, snow, temp, pet, pressure, lwd, g_rad, wind, s_vap, a_vap))

		solve(sol, air.cos_z)
	}
	

	sol : solver("Simply solver", inca_dascru, [2, hr], 1e-2)
	
	solve(sol, soil.water, gw.water, river.water, s1_2.water, s3_4.water)
	
}


