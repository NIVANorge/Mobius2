
model("EasyLake-SimplyQ") {
	
	extend("simplyq_model.txt") @exclude(connection()) #Exclude the downstream connection so that we can redeclare it with the epilimnion in it.
	
	epi      : compartment("Epilimnion")
	hyp      : compartment("Hypolimnion")
	
	ice      : quantity("Ice")
	
	wind     : property("Wind speed")
	g_rad    : property("Global radiation")
	pressure : property("Pressure")
	a_hum    : property("Actual specific humidity")
	rho      : property("Density")
	lwd      : property("Downwelling longwave radiation")
	sw       : property("Shortwave radiation")
	attn     : property("Attenuation coefficient")
	z_e      : property("Thickness")
	area     : property("Surface area")
	freeze_temp : property("Ice formation temperature")
	indicator : property("Ice indicator")
	
	evap_mm  : property("Evaporation per area")
	
	heat : quantity("Heat energy")
	
	downstream : connection("Downstream") @directed_graph {
		(river|epi)+ out
	} @no_cycles
	
	par_group("Lake data", epi) { # TODO: This could be local to EasyLake, and the rest of the modules could access it through epi.area
		A_surf : par_real("Lake surface area", [m 2], 107183, 0, 371e9)
	}
	
	load("modules/simplysoiltemp.txt", module("Simply soil temperature", air, soil, snow_box, snow, temp))
	load("modules/rivertemp.txt",      module("RiverTemperature", air, soil, river, water, heat, temp))
	load("modules/atmospheric.txt",    module("Atmospheric", air, temp, wind, g_rad, pressure, a_hum, rho, lwd))
	load("modules/airsea.txt",         module("AirSea", "AirSea Lake", air, epi, ice, temp, precip, wind, g_rad, pressure, rho, a_hum, lwd, sw, attn, freeze_temp, indicator,
											loc(A_surf), loc(epi.water), ht : loc(epi.water.heat), ht, loc(epi.water.temp)))
	load("modules/easylake.txt",       module("EasyLake", epi, hyp, water, heat, temp, z_e, freeze_temp, area, A_surf, loc(downstream)))
	
	solve(simply_solver, epi.water, hyp.water, epi.ice)
	
	lake_index : index_set("Lake")
	
	wb_index : index_set("Water body") @union(sc, lake_index)
	
	distribute(air, wb_index)
	distribute(snow_box, sc)
	
	distribute(epi, lake_index)
	distribute(hyp, lake_index)
	
}